#!/usr/bin/perl

use strict;
use warnings;
use diagnostics;
#use diagnostics -verbose;

use Digest::SHA;
use File::Basename;
use File::Spec;
use File::Path;
use File::Copy; 
use Time::HiRes qw( CLOCK_REALTIME gettimeofday tv_interval);
use Getopt::Long;
use POSIX; # needed for time formating
use Cwd qw(abs_path); # best way to get absolute paths

# Database Layer

use BerkeleyDB;
use BerkeleyDB::Hash;
use BerkeleyDB::Btree;

# Gives us tied access to databases

use MLDBM qw( BerkeleyDB::Hash Storable );

# Key function prototypes

sub debug($);


# Debug control


my $icDebug = 0;
my $tkdebug = 0;
my $tkflushdebug = 1;
my $gsDebug = 1;
my $duplicateDebug = 0;
my $granularSerialDebug = 0;    # Instruments the escape/unescape mechanism
my $serializeDebug = 0;			# Instruments the load/save at ghost level
my $gChildDebug = 0;
my $fseDebug = 0;
my $fseOjxDebug = 1;
my $fseSnapshotDebug = 0;

# Major globals

my $DESCRIPTOR_FILENAME = ".gfs_ip";
my $SERIALIZATION_VERSION_NUMBER = 5;
my $GENERAL_DEBUG = 1;
my $USUAL_IGNORE = 1;
local $main::SCRIPT_START = time();
local $main::SCRIPT_START_GTOD = [gettimeofday];
local $main::GUARD_TIME_SECONDS = undef; 

# Run statistics - globals

local $main::run_files_fullread = 0;
local $main::run_bytes_fullread = 0;
local $main::run_time_fullread = 0;
local $main::run_files_stat = 0;
local $main::run_descriptors_read = 0;
local $main::run_descriptors_write = 0;
local $main::readdir_invokes = 0;
local $main::readdir_entries = 0;
local $main::run_files_copied = 0;
local $main::run_bytes_copied = 0;
local $main::run_files_moved = 0;
local $main::run_db_puts = 0;
local $main::run_db_gets = 0;

# Codepatch tracking

my %codepathCount;

# Get path to our WD

my $HOME = $ENV{HOME};
my $WDIR = "$HOME/.gfs";
my $DBDIR = "$WDIR/db";
my $EDIR = "$ENV{HOME}/.gfsrun/EXEC-$$";

# These are the two "global" FSE objects used to resolve fs: and obj: LFNs

$main::localFileSystemFSE = undef;
$main::coreOjxFSE = undef; 

# The global database used everwhere

$main::coreGhostDatabase = undef; 

# Option management

my $OPT_ALL = 0;
my $OPT_STATS = 0;
my $OPT_GUARDTIME = undef;

&GetOptions( 'all' => \$OPT_ALL,
			 'G|guard=s' => \$OPT_GUARDTIME,
			 'stats' => \$OPT_STATS );


if( defined $OPT_GUARDTIME )
{
	$main::GUARD_TIME_SECONDS = $OPT_GUARDTIME;
	debug "Guard time set to: $main::GUARD_TIME_SECONDS\n";
}

# Utility function to present relative time in a human readable way

sub expressRelativeTime
{
	my $seconds = shift;

	if( $seconds > 24*60*60)
	{
		$seconds /= 24*60*60;
		my $r = sprintf( "%0.4s days", $seconds );
		return $r;
	}

	if( $seconds > 2*60*60)
	{
		$seconds /= 60*60;
		my $r = sprintf( "%0.4s hours", $seconds );
		return $r;
	}

	if( $seconds > 120)
	{
		$seconds /= 60;
		my $r = sprintf( "%0.4s min", $seconds );
		return $r;
	}

	return "$seconds" . "s";
}



# Helper function that creates a nested path with lots of error checking

sub mkdirs
{
    my $dir = shift;
    my $dirx = dirname $dir;

    # debug "MKDIRS:[$dirx]\n";
    eval{ mkpath($dirx,1) };
    $@ and die "Couldn't create dir path: $@";
    die "Did not find new path" if not -d $dirx;

   return 1;
}

# Helper function - if user requests it, ignores shit things like .git and .DS_Store
# Returns true if the file is good to go, or false if it is usually ignored
# This is not to be used for avoiding actual logic or recursion errors
# (such as scanning our own descriptors).
# User should be free to turn this feature off and the program must work as intended. 

sub fileIsUsuallyIgnored
{
	my $cfn = shift;

	return 0 if( not $USUAL_IGNORE );

	return 1 if $cfn =~ /\.DS_Store$/;
	return 1 if $cfn =~ /Thumbs.db$/;
	return 1 if $cfn =~ /\.pcip_info$/;

	return 0;
}

# Verifies readability of various directories needed for program invocation

sub verifyWorkingDirectories
{
	if( not -e $WDIR )
	{
	    mkdir $WDIR or die "Error creating [$WDIR]: $!";
	}

	if( not -e $DBDIR )
	{
	    mkdir $DBDIR or die "Error creating [$DBDIR]: $!";
	}

	if( not -e $EDIR )
	{
	    mkdirs($EDIR . "/") or die "Error creating [$EDIR]: $!";
	}

	die "Cannot create $WDIR" unless -w $WDIR;
	die "Cannot create $DBDIR" unless -w $DBDIR;
}

# Sets up our data

sub setupDatabases
{
	# my $bdb_env = new BerkeleyDB::Env 
	#     -Verbose => 1,
	#     -Home => $DBDIR,
	#     -Flags => DB_CREATE|DB_INIT_MPOOL|DB_INIT_CDB,
	#     -ErrFile => "$DBDIR/Errors-$$",
	#     -ErrPrefix => "GFS";

	my $gdb = GhostDatabase->new();
	$gdb->init();

	$main::coreGhostDatabase = $gdb;


}

sub debug ($)
{
	return if not $GENERAL_DEBUG;
	my $a = shift @_;
	chomp $a;
	print STDERR "D:" . $a . "\n";
}


sub CODEPATH($)
{	
	my $p = shift; 
	$codepathCount{$p}++;
	print( "CODEPATH: $p\n" );
}

sub reportCodepaths
{
	return if( not keys %codepathCount > 0 );

	print "--\n";
	print "Codepaths:\n";

	foreach my $p (sort keys %codepathCount )
	{
		print "$codepathCount{$p}\t$p\n";
	}
}

sub init
{
	verifyWorkingDirectories();
	setupDatabases();
	$main::localFileSystemFSE = new FilesystemExplorer::FS();
	my $ojx = FilesystemExplorer::ObjectStore->new( 'xx-GFS-ojx/test' );
	$ojx->setUpRepository();

	$main::coreOjxFSE = $ojx; 
}

# Incorporated code from String::Escape via per artistic license
# Reason: Need to tweak what counts as an escaped character

# =head1 AUTHOR
# Matthew Simon Cavalletto, C<< <simonm at cavalletto.org> >>
# Initial versions developed at Evolution Online Systems with Eleanor J. Evans and Jeremy G. Bishop.
# =head1 LICENSE
# Copyright 2010, 2002 Matthew Simon Cavalletto.
# Portions copyright 1996, 1997, 1998, 2001 Evolution Online Systems, Inc.
# You may use, modify, and distribute this software under the same terms as Perl.
# See http://dev.perl.org/licenses/ for more information.
# =cut


use vars qw( %Backslashed %Interpolated );

# Earlier definitions are preferred to later ones, thus we output \n not \x0d
_define_backslash_escapes(
	( map { $_ => $_ } ( '\\', '"', '$', '@' ) ),
	( 'r' => "\r", 'n' => "\n", 't' => "\t" ),
	( map { 'x' . unpack('H2', chr($_)) => chr($_) } (0..255) ),
	( map { sprintf('%03o', $_) => chr($_) } (0..255) ),
);

sub _define_backslash_escapes {
	%Interpolated = @_;
	%Backslashed  = reverse @_;
}

# $special_characters_escaped = backslash( $source_string );
sub backslash ($) {
	local $_ = ( defined $_[0] ? $_[0] : '' );
	# Preserve only printable ASCII characters other than \, ", $, and @
	s/([^\x20\x21\x24\x25-\x39\x41-\x5b\x5d-\x7e])/\\$Backslashed{$1}/gs;
	return $_;
}

# $original_string = unbackslash( $special_characters_escaped );
sub unbackslash ($) {
	local $_ = ( defined $_[0] ? $_[0] : '' );
	s/ (\A|\G|[^\\]) [\\] ( [0]\d\d | [x][\da-fA-F]{2} | . ) / $1 . ( $Interpolated{lc($2) }) /gsxe;
	return $_;
}


sub gfs_escape($)
{
	my $v = shift;

	return "_" if not defined $v;

	$v =~ s/\:/\\c/gs;
	$v =~ s/\@/\\a/gs;
	$v =~ s/\#/\\p/gs;
	$v =~ s/\n/\\n/gs;
	$v =~ s/\_/\\l/gs;

	$v =~ s/([^\x20\x21\x24\x25-\x39\x41-\x5b\x5d-\x7e])/"\\x(" . unpack('H2', $1) . ")" /gse;


	return $v;

}

sub gfs_unescape($)
{
	my $v = shift;

	return undef if( $v eq "_");


	$v =~ s/\\x\(([\da-fA-F]+)\)/ pack('H2', $1) /gse;


	$v =~ s/\\c/:/gs;
	$v =~ s/\\a/\@/gs;
	$v =~ s/\\p/\#/gs;
	$v =~ s/\\n/\n/gs;
	$v =~ s/\\l/\_/gs;



	return $v;
}

# test cases:
# snapshot directories both a/* and a/ and a

# insight: we could have an entry for a directory.. the last full time it was scanned
# then if the read date of the file is earlier than the scan date of the directory
# then we know the file may be stale and can delete it



# how do we stick virtual things into the hash?
# for instance, where or how do we store the idea that a .tar file has certain contents
# or that an rsync directory (jgilbert@auspice.net:a/b/c) contains a file?
# almost like a normal path a/b/c is basically an implicit "local:a/b/c"..
# the local handler knows to write .gfs_info files
# whereas the remote one knows not to
# each FS type offeres a get, move, copy, retrieve full checksum, stat, etc function
# so now we can handle S3, Rsync, etc

# how does that tie in with tags?
# how do I say anything under /Volume/Foo should be addressed as FOODRIVE:
# clearly this only matters during AFN conversion
# disktag:FOODRIVE|/one/two/three
#
# if that is the AFN recorded, then the system will think that is there
# we just need to write a record like /Volume/Foo -> FOODRIVE
# and also write a /Volume/Foo/GFS_ID.txt file
# then a dereference just requires that we verify that the /Volume/Foo/GFS_ID can still be reached
# maybe there is a tool like "bless" which does this for you
# so the rel->AFN conversion would either need to rely on this mapping
# or traverse back up to make sure that all ../GFS_ID.txt files were registered

# is a snapshot treated as a file collection as a target? if it is, we have to emphasize 
# that it is a ghost. e.g. you can enumerate, but the fact that snapshot:4444/sdf contains
# this file version could never be used as a proxy.
# so when you "get all AFNs for hash", you'd have to pass to a FShandler for the snapshot

# GFS is an overlay snapshoting and directory reconstruction tool. Its not really meant 
# for version control, but managing someone's life which may be scattered 
# across many different asset types and locations.
# GFS is entirely built around command line tools. It doesn't watch the filesystem
# for changes.
# key features
# 1) highly accelerated content-aware rsync - a file is never retransmitted unless needed
# 2) worried you still wantt o eb sure you have a backup of a wedding photos? 
#     this has tools that let you flag and manage bit-rot and verify your stuff is where
#     you think it is
# 3) work with large file trees both on a desktop and laptop? GFS can tell you instantly
#     which chnages you've made on your laptop that are not yet on your desktop and push them
#     to the desktop efficiently
# 4) in the field and want to be sure files you are working on are protected?
#     GFS can make sure any file is either in your dropbox, on your machine at home
# 5) trying to free up space on your laptop? do you have a copy of that big 10 GB ISO file
#     at home? find out quickly if you can ditch it. 



# Database layer use cases
# 1) quickly give me ghost information for a path
#      AFN -> GhostSerialized as absolute
# 2) quickly give me the AFNs associated with a hash or size
#      hash -> AFN 0..n   (needed for LSDup)
#	   size -> AFN 0..n   (also possibly needed for LSDup)

# so when something is stored in location X or found in location X, 
# what permanent record is created?
# maybe we create spontaneous snapshot records that can be loaded back in?
#
#    gfs rsync /source jgilbert@asdf:foobar/
# 
# All files written to foobar drops a file in .gfs/receipts/rsync-$$-date.snapshot
# With the note: "This snapshot record was written by GFS on date XXXX"

sub main()
{
	# print "Welcome to Ghost.\n";

	init();

	# TODO: Some kind of sanity checking on arguments
	# Are all the files listed MECE?
	# Just a warning is fine here but the user ought to be prompted
	# just to be sure

	my $tk = TruthKeeper::get();

	my $verb = shift @ARGV;

	handleVerb( $verb );

	FSEFactory->shutdown();

	$tk->flush();

	if( $OPT_STATS )
	{
		handleStats();
	}

	reportCodepaths();
}


sub handleStats
{
	print "----- DONE -----\n";

	print "Full reads count: $main::run_files_fullread\n";
	print "Full reads size : $main::run_bytes_fullread\n";
	print "Full read time  : $main::run_time_fullread\n";

	print "Stat reads      : $main::run_files_stat\n";

	print "Local .gfs reads: $main::run_descriptors_read\n";
	print "Local .gfs writs: $main::run_descriptors_write\n";
	print "Readdir invokes : $main::readdir_invokes\n";
	print "Readdir entries : $main::readdir_entries\n";

	print "Files copied    : $main::run_files_copied\n";
	print "Bytes copied    : $main::run_bytes_copied\n";
	print "Files moved     : $main::run_files_moved\n";

	print "Database puts   : $main::run_db_puts\n";
	print "Database gets   : $main::run_db_gets\n";

	print "Total Script Time: " . tv_interval( $main::SCRIPT_START_GTOD ) . "\n";	
}

sub handleVerb
{
	my $verb = shift;

	if( $verb eq "lsdup")
	{
		doverb_lsdup();
		return;
	}

	if( $verb eq "help")
	{
		doverb_help();
		return;
	}

	# Simple harness that confirms we can read and write to the database

	if( $verb eq "dbtest")
	{
		doverb_dbtest();
		return;
	}


	# Interface into the pointer storage and retrieval system

	if( $verb eq "pointer" )
	{
		doverb_pointer();
		return;
	}

	# The "store" verb basically pushes the contents to the location specified
	# typically as a copy operation. Originally intended to store files into
	# the OJX. (Full semantics have not been worked out.)

	if( $verb eq "store" )
	{
		doverb_store();
		return;
	}

	# The "mv" verb calls the move handlers for the relevant FSE

	if( $verb eq "mv" )
	{
		doverb_move();
		return;
	}

	# LS tells us what we can find out about the current directory tree
	# nothing is force read. there is a dirscan and that is it.

	if( $verb eq "ls" )
	{
		doverb_ls();
		return;
	}

	# Asks the question if the target is exactly identical to the source
	# and if not, shows the difference.

	if( $verb eq "compare" )
	{
		doverb_compare();
		return;
	}

	if( $verb eq "checksum" )
	{
		doverb_checksum();
		return;
	}
	# Writes a permanent file of this entire tree

	if( $verb eq "snapshot")
	{
		doverb_writeSnapshot();
		return;
	}

	if( $verb eq "rsync")
	{
		doverb_rsync();
		return;
	}

	die "Verb $verb not recognized - try $0 help for ideas";

	return;
}




sub doverb_help
{
	print <<HERE;

gfs - a universal tool for file handling with checksums

Moves, snapshots and searches for duplicates with a minimum of disk activity.

Unless otherwise instructed, GFS avoids disk access whenever possible. If
meta-data about the size, modification date, or checksum is already available,
it is used for the operation regardless of its age (although you might get a
warning if its too old.) You can force information to be more up to date by
setting a guard time with -G. This is the number of seconds old that metadata
is valid (either stat or hash). Setting guard to 0 means that basically no
metadata caching is done, the most conservative (and slowest) approach.

Future feature: Checksums are not typically forced by the -G switch unless
there is some reason to doubt them (such as a changed size or modification
date. If you really want everything recalculated, set the --force-checksum.)

gfs ls file1 [... file-n]

	Prints out each file and the data known about it. Does not force any new
	calculations or checksums unless a guard argument is in place.

gfs rsync [source1 .. source-n] <target>

	Moves or copies files so that target looks exactly like the merger
	of its sources. Mostly implemented.

gfs compare [source1 .. source-n] <target>

	Checks if two directories contain exactly the same files. A report is
	generated for spurious, incorrect, missing and matched files. Directories
	are not quite implemented properly yet.

gfs snapshot file1 [... filen]

    Writes a file describing the current state of the listed files. Checksums
    / stats if not present are forced, otherwise, no unnecessary disk activity
    is created.

gfs lsdup [source1 .. source-n] target [--all]

    For every file in target tree, reports which is duplicated
    in the target or source trees.

    --all forces a report for every file, not just duplicates (not yet
      implemented)

HERE

}



# doverb_dbtest
#
# Simple actions for using the ghost database.

sub doverb_dbtest()
{
	my $afn = 'fs:root:testfile1';
	my $gdb = GhostDatabase->getCore();
	my $tk = TruthKeeper::get();

	my $g = $tk->obtainGhostForLFN( $afn );
	$gdb->storeGhostToAFN( $g, $afn );
	my $ng = $gdb->findGhostByAFN( $afn );

	$ng->printDebug();



}

# doverb_store
#
# Simple actions for using the OJX. 

sub doverb_store()
{
	my $origArg = @ARGV;

	my $name = shift @ARGV;
	my $value = shift @ARGV;

	my $tk = TruthKeeper::get();

	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( $value );
	my ($sourceGS, $sourceStash) = $tk->makeGhostsFromCommandLine( $name );

	my( $targetUserFN, $targetFqlfn, $targetGhost ) = @{$targetStash->[0]};
	my( $sourceUserFN, $sourceFqlfn, $sourceGhost ) = @{$sourceStash->[0]};

	$targetGhost->store( $sourceGhost );
}




sub doverb_move()
{
	my $origArg = @ARGV;

	die "Must give EXACTLY 2 arguments" if( $origArg != 2 );

	my $src = shift @ARGV;
	my $dest = shift @ARGV;

	my $tk = TruthKeeper::get();

	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( $dest );
	my ($sourceGS, $sourceStash) = $tk->makeGhostsFromCommandLine( $src );

	my( $targetUserFN, $targetFqlfn, $targetGhost ) = @{$targetStash->[0]};
	my( $sourceUserFN, $sourceFqlfn, $sourceGhost ) = @{$sourceStash->[0]};

	$sourceGhost->moveGhost( $targetGhost );
}



# doverb_pointer
#
# Simple actions for using the OJX. 

sub doverb_pointer()
{
	my $origArg = @ARGV;

	my $name = shift @ARGV;
	my $value = shift @ARGV;

	my $ojx = $main::coreOjxFSE;

	print "ARGV=@ARGV ---$origArg\n";

	die "Too many arguments" if( $origArg > 2 );

	if( $origArg> 1 )
	{
		print "Pointer [$name] now set to [$value]\n";
		$ojx->setPointer( $name, $value );
		return;
	}

	if( $origArg > 0 )
	{
		
		$value = $ojx->getPointer( $name );
		print "Pointer [$name] is currently: [$value]\n";
	}

}

# doverb_writeSnapshot - handle the write-snapshot command
#
# for each item listed, shows what we know about it -- no recursion, no forced 
# characterization

sub doverb_writeSnapshot()
{
	my $tk = TruthKeeper::get();
	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( @ARGV );

	my $i = $targetGS->getIterator();

	my $targetFilename = "snapshot-$$.out";
	my $targetLFN = assembleLFN( "snap", $targetFilename, "" );
	my $targetRoot = $tk->obtainGhostForLFN( $targetLFN );
	my $targetFSE = FSEFactory->getFSEHandlerForGhost( $targetRoot );

	while( my $g = $i->next() )
	{
		debug "Write Snapshot - Got Ghost: [$g]\n";

		if( not $g->canHaveChildren() )
		{
			$g->forceCharacterization();
		}

		# $g->printDebug();

		$targetFSE->absorb($g);

		# $store->addGhost( $g );
	}

	$targetFSE->finalize();

	# $store->writeSnapshotToDisk( "snapshot-$$.out");
}



sub doverb_compare()
{
	my $target = pop @ARGV;
	my @source = @ARGV;

	my $tk = TruthKeeper::get();
	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( $target );
	my ($sourceGS, $sourceStash) = $tk->makeGhostsFromCommandLine( @source );
	
	my( $targetUserFN, $targetFqlfn, $targetGhost ) = @{$targetStash->[0]};

	if( not $targetGhost->exists() )
	{
		die "Cannot stat() target: $targetUserFN! $!";
	}

	die unless $targetGhost->canHaveChildren();

	my ($targetHandler, $targetRoot, $targetPath)  = $targetGhost->getLFNcompontents();

	my $gst = new GhostStateTransform();

	my $targetGSiterator = $targetGS->getIterator();
	while( my $g = $targetGSiterator->next() )
	{	
		$gst->setOriginal($g);
	}

	my $sourceGSiterator = $sourceGS->getIterator();
	while( my $g = $sourceGSiterator->next() )
	{
		my $sourceLFN = $g->lfn();
		my $sourcePath = $g->getPath(); 
		my $basename = basename $g->getPath();
		my $newPath = File::Spec->catfile( $targetPath, $basename );
		my $targetLFN = main::assembleLFN( $targetHandler, $targetRoot, $newPath );

		print "--- INITIAL SCAN -------------------\n";
		print "[$sourcePath] -> [$newPath]\n";
		#print "Thing I'm iterating on:\n";
		#$g->printDebug();

		$gst->setTargetState( $targetLFN, $g );
	}

#	$gst->printDebug();
#	$gst->printDebugWithPaths();

	$gst->doforeachLFN( sub
	{
		my $self = shift;
		my $lfn = shift;

		my $status = "";

		if( $gst->LFNtest_isComplete($lfn) )
		{	
			$status = "Match";
		}

		if( $gst->LFNtest_isMissing($lfn) )
		{	
			$status .= "Missing";
		}

		if( $gst->LFNtest_isSpurious($lfn) )
		{	
			$status .= "Spurious";
		}

		if( $gst->LFNtest_isIncorrect($lfn) )
		{	
			$status .= "Incorrect checksum";
		}

		print "$lfn -> $status\n";
	});


}








# doverb_ls - handle the LS command
#
# for each item listed, shows what we know about it -- no recursion, no forced 
# characterization

sub doverb_ls()
{
	my $tk = TruthKeeper::get();

	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( @ARGV );

	foreach my $tuple (@$targetStash)
	{
		my( $userFN, $fqlfn, $g ) = @$tuple;

		# $g->printDebug();

		my $fullHash = $g->fullHash() || '<not calculated>';
		my $lastFullHash = $g->lastFullHash();

		my $lfh_string = (defined $lastFullHash) ? strftime( "%c", localtime( $lastFullHash) ) : '<not calculated>';

		my $d = (defined $lastFullHash) ? expressRelativeTime( $main::SCRIPT_START - $lastFullHash ) : "x";
		my $type = $g->getType() || '?';

		$lfh_string .= "($d)";

		print +(join "\t", $type,$fullHash,$lfh_string,$userFN) . "\n";
	}
}


# doverb_checksum - checksum the files we are given
#
#

sub doverb_checksum()
{
	my $tk = TruthKeeper::get();
	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( @ARGV );
	my $i = $targetGS->getIterator();



	while( my $g = $i->next() )
	{
		my $lfn = shift; 



		my $fullHash = $g->getFullHash();
		# $g->printDebug();

		print +(join "\t", $fullHash, $g->lfn()), "\n";
	}
}




## A. There is a file fT already sitting at TARGET at this filename
## 	A1. The hash of fT is correct - its what we want
## 		-- Mark TARGET fT as claimed, no action needed, go to next file
## 	A2. The hash is not correct
## 		-- Mark as orphaned, select new orphaning location - during clearance MOVE phase (will be moved out first)
## 		-- Flow down to Case B
## B. There is no file at the remote location in TARGET tree at that filename, or the file in TARGET is marked orphaned
## 	B1. There is no hash ANYWHERE at TARGET, PILLAGE, AUX, COPY or ORPHAN
## 		-- Queue for remote copy, function done
## 	B2. There is a hash available in PILLAGE that is not claimed
## 		-- Queue move PILLAGE -> fT, claim it
## 	B2. There is a hash availble in ORPHAN that is not claimed
## 		-- Queue clearance move ORPHAN -> fT (assign new location) - DONE  (What about case of a swap..?)
## 	B3. There is a hash available in TARGET that is claimed
## 		-- Queue local copy, done
## 	B4. There is a hash available in AUX
## 		-- Queue local copy, done
## 	B5. There is a hash available in PILLAGE that is claimed
## 		-- Queue local copy, done
## 	B6. There is a hash available in COPYLIST
## 		-- Queue local copy, don## 


## Core test cases
##
## Assemble target such that it has
##
## File0 - An immoveable file
## File1 - A   # file in right place
## File2 - B   # file needs a rename nad has to get out of the way
## File6 - F   # orphaned in the new tree, no winkins
## File-x1 - J # pure swap case
## File-x2 - I # pure swap case
## File9x - K  # make intentional orphan with data K
## 
## Move into it
##
## dir1/File3 - C # input t
## dir2/File3 - D # input file that overlaps other input file
## dir1/File4 - E # input file that that is a net new hash
## dir1/File1 - A # target file already in the right place
## dir1/File8 - E # two input files with same hash (trigger local move, 1x copy)
## dir1/File2 - H # net new file overwriting a existing file who is needed elsewhere 
## dir1/File5 - B # needs to pull fom the orphan
## dir1/File-x1 - I # pure swap case
## dir1/File-x2 - J # pure swap case
## dir1/File9x - A # make an orphan
## dir1/File9a - K # one of the places that 9x can go
## dir1/File9b - K # a place for a copy of a wink in
## dir1/File9c - K # a place for a copy of a wink in




## also need to add cases where the orphan is actually a whole directory
## e.g. a new directory replacing old directory,
## and a new file replacing a directory, and a directory replacing a file

## A case where we run out of wink-ins





## iterate through all files in source
## if file is present, and correct - CLAIMED
## if file is present, and wrong - ORPHAN, add to PENDING
## if file is not present add to PENDING

# now go through all PENDING, assigning source LFNs
# if we can find an unclaimed orphan, queue a local move on the CLEARANCE list
# if we can find an calimed orphan, queue a local move on the ORPHANCOPY list
# if we can find a target, move onto the LOCAL copy

# first do all ORPHANCOPY operations
# Then CLEARANCE moves
# Then any orphans left, move out
# then finally transfer the PENDING items

# I think this will FAIL to handle a swap. Tower of hanoi problem.
#


# Go through all files in source and claim those paths in the destination
# A1- if the file is present, and correct - mark destpath=DONE
# A2- if the file is present, and incorrect - mark destpath=WRONGFILE
# A3- if the file is not present - mark DESTPATH=EMPTY

# now move all orphans to new location. If we can find any WRONGFILE slots they fit, 
# move them there. Every move should update destpath to DONE

# otherwise, give them new names .orphan. check the new names to make sure they don't have destpath
# 

sub doverb_rsync
{
	my $target = pop @ARGV;
	my @source = @ARGV;

	my $tk = TruthKeeper::get();
	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( $target );
	my ($sourceGS, $sourceStash) = $tk->makeGhostsFromCommandLine( @source );
	

	my( $targetUserFN, $targetFqlfn, $targetGhost ) = @{$targetStash->[0]};

	if( not $targetGhost->exists() )
	{
		die "Cannot stat() target: $targetUserFN! $!";
	}

	die unless $targetGhost->canHaveChildren();

	my ($targetHandler, $targetRoot, $targetPath)  = $targetGhost->getLFNcompontents();

	my $gst = new GhostStateTransform();

	my $targetGSiterator = $targetGS->getIterator();
	while( my $g = $targetGSiterator->next() )
	{	
		$gst->setOriginal($g);
	}

	$gst->printDebug();

	my $sourceGSiterator = $sourceGS->getIterator();
	while( my $g = $sourceGSiterator->next() )
	{
		my $sourceLFN = $g->lfn();
		my $sourcePath = $g->getPath(); 
		my $basename = basename $g->getPath();
		my $newPath = File::Spec->catfile( $targetPath, $basename );
		my $targetLFN = main::assembleLFN( $targetHandler, $targetRoot, $newPath );

		print "--- INITIAL SCAN -------------------\n";
		print "[$sourcePath] -> [$newPath]\n";
		#print "Thing I'm iterating on:\n";
		#$g->printDebug();

		$gst->setTargetState( $targetLFN, $g );
	}

	$gst->createClearanceMoves();
	print "******************* Post Clearance\n";
	$gst->printDebug();
	print "******************* Post Clearance\n";
	$gst->printDebugWithPaths();


	$gst->handlePillageFromGS( $gst->orphanGS() );

	print "******************* Post Orphan Pillage\n";
	$gst->printDebug();
	print "******************* Post Orphan Pillage\n";
	$gst->printDebugWithPaths();


	$gst->handleLocalCopyFromGS( $gst->obtainCurrentStateAsGS() );

	print "******************* LocalCopy \n";
	$gst->printDebug();
	print "******************* LocalCopy\n";
	$gst->printDebugWithPaths();


	$gst->handleLongHaulCopyFromGS( $sourceGS );

	print "******************* Long Haul \n";
	$gst->printDebug();
	print "******************* Long Haul \n";
	$gst->printDebugWithPaths();


	my $xgs = $gst->obtainCurrentStateAsGS();



	$gst->handleLocalCopyFromGS( $xgs );


	$gst->printWorklist();
	$gst->verifyCorrectness();

	#$gst->printDebug();

	# at this point, we wantt the GST to generate all ghosts (in present locations)
	# currently under the tree
	# iterate over all current ghosts

}


###########
# Lsdup: A B C - for every file in C, say which are copied in A and or B and C (and where)
# Ghost-tools tries to be minimal about hitting the disk, but at a minimum,
# a duplicate requires that at some point GFS has to have a full SHA-1 match of the file.
# 
# Tells you where the duplicates are, and when they were last checked or verified
#
# foreach c (where size != 0)
# c->findDuplicates( $g );
# b->findDuplicates( $g );
# a->findDuplicates( $g );
# findDuplicates psucdocode
#    get ghost G for FN
#    find candidates - $fse->listAllFilesWithSize( $size )
#        lafby size, first time its called iterates over all FSE, hashing up sizes
#		 next, for each we do the test "$g->hasIdenticalContent($g)"
#		 	HIC first rules out size, then mod-date, then finally fullhash

sub doverb_lsdup()
{
	my $target = pop @ARGV;
	my @source = @ARGV;

	my $tk = TruthKeeper::get();
	my ($targetGS, $targetStash) = $tk->makeGhostsFromCommandLine( $target );
	my ($sourceGS, $sourceStash) = $tk->makeGhostsFromCommandLine( @source );
	my $i = $targetGS->getIterator();

	my $dup_count = 0;
	my $dup_size = 0;
	my $dup_size_interior = 0;
	my $dup_count_interior = 0;
	my $total_files = 0;
	my @report; 
	my $asWeGoPrint = 1;

	while( my $g = $i->next() )
	{
		my $lfn = $g->lfn();

		next unless $g->isFile();
		next if fileIsUsuallyIgnored( $lfn );

		$total_files++;

	#	print "LS Dup -handling LFN:[$lfn]\n";

		my @tdups = $targetGS->findOtherDuplicates( $g );
		my @sdups = $sourceGS->findOtherDuplicates( $g );

		my $dupCount = @tdups + @sdups;

		if( $dupCount > 0 )
		{
			$dup_count_interior++;
		}

		if( $asWeGoPrint )
		{
			printf "[%3d]_________%s\n", $dupCount, $lfn;
		}

		if( @tdups > 0 )
		{	
			#print "    Duplicate in local tree: ($target)\n";

			foreach my $d (@tdups)
			{
				my $dfn = $d->lfn();
 
				print "      Target: $dfn\n" if $asWeGoPrint;
				$dup_count++;
				$dup_size += $d->getSize();
				$dup_size_interior += $d->getSize();
				# $d->printDebug();
			}
		}

		if( @sdups > 0 )
		{
			$dup_size_interior += $g->getSize();
			#print "    Duplicate in source trees (@ARGV):\n";
			foreach my $d (@sdups)
			{
				my $dfn = $d->lfn();

				print "      Source: $dfn\n" if $asWeGoPrint;
				$dup_count++;
				$dup_size += $d->getSize();
				# $d->printDebug();
			}
		}

		push @report, [$g, \@tdups, \@sdups];
	}

	print "Duplicate report: size X (target dups / source dups ) filename\n";

	foreach my $line (reverse sort { $a->[0]->getSize() <=>  $b->[0]->getSize() } @report )
	{
		my ($g, $tdups, $sdups ) = @$line;
		my $file = $g->lfn();
		my $size = $g->size();
		my $i = 0 + @$tdups;
		my $e = 0 + @$sdups;

		if( $i > 0 or $e > 0 or $OPT_ALL )
		{	
			print "$size X ($i/$e) $file \n";
		}
	}

	print "LSDup finished\n";
	print "I found $dup_count_interior files that were duplicated in target ($target),\n";
	print "when searching among the target plus source files: @ARGV\n";
	print "occupying $dup_size unnecessary space across the whole filesystem over $dup_count files.\n";
	print "Within the target itself, this represents $dup_size_interior bytes.\n";
	print "There were $total_files in the target directory\n";
}

# Write the in place descriptors in each directory named

sub doverb_writeDescriptor()
{
	die "dead";
	my $store = GhostStore->new();

	my $fse = new FilesystemExplorer::FS();
	$fse->setRoots( @ARGV );

	# sort of a lazy way to do it -- forces
	# a checksum computation, which should
	# populate the in place descriptors

	# a better way would be to enter each directory
	# and call some specific "build for x" function

	while( defined( my $file = $fse->next() ) )
	{
		print "Verb WD - Scanning: [$file]\n";

		if( -f $file )
		{
			$fse->forceGhostCharacterization( $file );
			my $fullHash = $fse->getFullHash( $file );
			print "Verb WD: fullhash = $fullHash\n";
		}
		else
		{
			print "Verb WD - Not a regular file: [$file]\n";
		}

	
	}
}

# A hard ignore is a filename that we must pretend never existed
# it will not factor into any directory checksums
# or be saved to any ghost stores

sub isHardIgnore
{
	my $fn = shift;

	die "Assert: $fn should be a path" if not main::assert_regularpath( $fn );

	my $b = basename $fn;	

	return 1 if $b =~ /$DESCRIPTOR_FILENAME$/;

	return 0;
}


# Sanity check that a path is indeed canonicalized 
#
# GFS relies on all paths being canonicalized so that text matching occurs
# properly. Otherwise ./foo and foo will not text match and it will appear
# like they are different files. Any routines that brings in a "net new" path
# to the system should convert it to a cpath.

sub assert_cpath($)
{
	my $lfn = shift;
	assert_regularpath($lfn);
	my $clfn = File::Spec->canonpath( $lfn );
	die "ASSERT CPATH FAILED: $lfn ne $clfn" if $lfn ne $clfn; 
}

# Sanity check that in places where an operation is to work
# on a regular path that it actually is one.

sub assert_regularpath($)
{
	my $path = shift;
	my $c = () = $path =~ m/\:/go;
	#print "Testing [$path] - got $c\n";
	die "ASSERT regularpath [$path] - failed -- smells like a FQLFN" if( $c == 2 );
	return 1; 
}

# Returns a fully cleaned up absolute path name for this file
# Initial testing has shown this handles foo/../../ properly, etc
# probably requires a ton of stats in cases where there are ..
# but doesn't appear to be too onerous for usual path names

sub rewriteLFNPathAbsolute
{
	my $lfn = shift;
	die unless defined $lfn;
	my( $handler, $root, $path ) = parseFQLFN( $lfn );
	die "only fs roots can be made into absolute paths ($lfn)-($root)" unless $handler eq 'fs';
	$path = abs_path( $path );
	return assembleLFN( $handler, $root, $path );
}

sub rewriteLFNreplaceRoot
{
	my $lfn = shift;
	my $new = shift;

	die unless defined $lfn;
	die unless defined $new; 

	my( $handler, $root, $path ) = parseFQLFN( $lfn );
	$root = $new;
	return assembleLFN( $handler, $root, $path );
}

# Takes an LFN and removes the leading directory from the path component

sub rewriteLFNPathToBasename
{
	my $lfn = shift;

	my( $handler, $root, $path ) = parseFQLFN( $lfn );
	my( $fn, $dir ) = fileparse( $path );
	return assembleLFN( $handler, $root, $fn );
}

# Takes an LFN and adds a leading directory portion onto the path

sub rewriteLFNwithLeadingDirectory
{
	my $lfn = shift;
	my $leadingDir = shift;

	my( $handler, $root, $path ) = parseFQLFN( $lfn );
	$path = File::Spec->catfile( $leadingDir , $path );
	$path = main::canonize( $path );
	return assembleLFN( $handler, $root, $path);
}

# Pass it a path (not a FQLFN) and you'll get the canonical version
# which can be file compared

sub canonize($)
{
	my $lfn = shift;

	assert_regularpath( $lfn );

	my $clfn = File::Spec->canonpath( $lfn );

	# print "Canonicalize: $lfn ---> $clfn\n";	

	return $clfn;
}

# Assembles a FQLFN from its consituent parts

sub assembleLFN
{
	my ($handler, $root, $path ) = @_;

	die unless length $handler >0;
	die unless defined $root;
	die unless defined $path;

	return join ':', $handler, $root, $path;
}


# Splits out a FQLFN into its three parts

sub parseFQLFN
{
	my $lfn = shift;

#	my @parts = split( ':', $lfn );
	my (@parts) = ( $lfn =~ m/^(\w+)\:([^:]*)\:(.*)/ );

	my ($handler, $root, $path ) = @parts;

	die "assert: parseFQLFN [$lfn] - does not have 3 parts!" unless scalar(@parts) == 3;
	# print "ParseLFN = [$lfn] -> handler:[$handler] root:[$root] lfn:[$path]\n";

	return @parts; 
}

# Does the gueswork of taking a partial user provided LFN and figuring
# out the intent and fully qualifying it

sub fullyQualifyLFN
{
	my $lfn = shift;

	my @c = split( ':', $lfn );

	my $handler;
	my $root;
	my $path;

	# first handle the case of bareword stuff like ./quxx and bar or testa/file1.txt

	if( 0 )
	{
		print "fullyQualifyLFN: Testing for element count";
		print scalar @c;
		print "\n";
	}

	if( scalar(@c) == 1 )
	{
		$handler = "fs";
		$root = "root";
		$path = $lfn;
	}
	elsif( @c < 3 )
	{
		# Yucky case - user has given us 2 things, what does he or she mean?
		# first, lets see if the first thing is one of our keywords
		# if so, we can usually assume the next part is the root

		if( $c[0] =~ /^fs|snap|remote|obj|vol/ )
		{
			$handler = $c[0];
			$root = $c[1];
			$path = "";

			warn "Interpolating LFN -- did you mean to leave off a path? Assuming your intent was handler:[$handler] root:[$root] path:[$path] ";
		}
		else
		{
			# In this case, best to assume that the first thing is the root
			# but what handler to use?

			$handler = "";
			$root = $c[0];
			$path = $c[1];

			die "Cannot infer compound path - [$lfn] - please specify a handler";
		}
	}
	elsif( @c > 3 )
	{
		die "Ambiguous - too many colons in this filename. (Sorry we don't like colons in filenames, at least not when provided on the command line)";
	}
	else
	{
		($handler, $root, $path ) = @c;
	}

	$path = main::canonize( $path );

	# debug "FULLY QUALIFY LFN:[$lfn] -> handler:[$handler] root:[$root] path:[$path]\n";

	return assembleLFN( $handler, $root, $path );
}


# Stat 

sub statModeGen_error
{	
	my $errno = shift; 
	return "ERR/" . $errno;
}

sub statModeParse_isError
{
	my $stat = shift;
	return 1 if $stat =~ /^ERR/;
	return 0; 
}



# Hash parsing tools live here

# Breaks apart a hash into its three components. 

sub extractHashComponents
{
	my $hashString = shift;

	my ($algorithim, $meta, $digest ) = split '/', $hashString;

	die unless defined $algorithim;
	die unless defined $meta;
	die unless defined $digest;

	return [ $algorithim, $meta, $digest  ];
}

sub globalMkPath
{
	my $p = shift; 
	debug "Mkpath: $p";
	File::Path::make_path( $p );

	$@ and die "Couldn't create dir path: $@";
    die "Did not find new path" if not -d $p;

}


# Encapsulation around a fast disk-based database of ghost information

package GhostDatabase;

use BerkeleyDB;
use BerkeleyDB::Hash;
use BerkeleyDB::Btree;

sub new
{
	my $class = shift;
	my $self = {};
	bless $self, $class;
	return $self; 
}

sub debug($)
{
	my $a = shift;
	main::debug( "GDB: " . $a );
}




sub getCore
{
	my $class = shift;

	return $main::coreGhostDatabase;
}


sub init
{
	my $self = shift;

	my $DBDIR = "db-test";

	main::globalMkPath( $DBDIR );

	my $bdb_env = new BerkeleyDB::Env 
	    -Verbose => 1,
	    -Home => $DBDIR,
	    -Flags => DB_CREATE|DB_INIT_MPOOL|DB_INIT_CDB,
	    -ErrFile => "$DBDIR/Errors-$$",
	    -ErrPrefix => "GFS"
	    ;

	die "Environment failed: $!, $BerkeleyDB::Error" unless defined $bdb_env;

	my $afnToGhost = new BerkeleyDB::Btree
	    -Flags    => DB_CREATE,
	    -Env => $bdb_env,
	    -Filename => "afn-to-ghost.db";

	die "Failed to create afnToGhost: $!, $BerkeleyDB::Error" unless $afnToGhost;

	my $status = $afnToGhost->db_put("foo","bar");
	ckStatus( $status );

	$self->{AFN_TO_GHOST_DB} = $afnToGhost;
}

sub ckStatus
{
	my $status = shift;

	die "DB error: $!, $BerkeleyDB::Error" if $status; 
}

sub findGhostByAFN
{
	my $self = shift;
	my $afn = shift;

	if( exists $self->{AFN_TO_GHOST_CACHE}->{$afn} )
	{
		# return $self->{AFN_TO_GHOST_CACHE}->{$afn};
	}


	my $value;

	my $status = $self->{AFN_TO_GHOST_DB}->db_get( $afn, $value );
	# ckStatus( $status );
	$main::run_db_gets ++;

	if( defined $value )
	{
		print "Got from DB: $value\n";
		my $ghost = Ghost->newFromSerialized( $value );
		$self->{AFN_TO_GHOST_CACHE}->{$afn} = $ghost; 
		return $ghost;
	}

	# Guess we have to make it. This call to make_private() is only supposed
	# to happen in one place inside the TruthKeeper but I think its OK since
	# i'm not sure I want these things in the TK cache. 

	my $gAFN = Ghost->make_private( $afn );

	$self->{AFN_TO_GHOST_CACHE}->{$afn} = $gAFN;

	return $gAFN; 

}

sub storeGhostToAFN
{
	my $self = shift;
	my $g = shift;
	my $afn = shift;

	my $serialized = $g->serialize();

	print "GDB:STORE: $serialized\n";

	my $status = $self->{AFN_TO_GHOST_DB}->db_put( $afn, $serialized );
	ckStatus( $status );

	$main::run_db_puts ++;
}



sub shutdown
{
	my $self = shift; 
	my $status = $self->{AFN_TO_GHOST_DB}->db_close();
	ckStatus( $status );
}

	# my $gdb = GhostDatabase->getCore();
	# my $absGhost = $gdb->findGhostByAFN( $afn );
	# $absGhost->learnFromGhost( $g );





# This holds everything that represents the core stat metadata of an object
# except for size, mtime, and mode.  Anything that should be in lock-step with
# these stat fields goes in here. This is really an optimization to prevent
# new fields from unnecessarily being added into ghosts as our concept of
# stat-like metadata expands over time. Metadata that is not lock-tied to the
# stat(3) call should NOT be added here.


package StatStruct;

sub new
{
	my $class = shift;
	my $self = [];
	bless $self, $class;
	return $self; 
}

sub debug($)
{
	my $a = shift;
	main::debug( "statStruct: " . $a );
}

sub loadFromPOSIXstat
{
	my $self = shift;
	my $arr = shift;

	# ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,$atime,$mtime,$ctime,$blksize,$blocks)

	print "StatStruct: Loaded :" . (join ',', @$arr ) . "\n";

	# drop 2,7,9

	splice( @$arr, 2, 1 );
	splice( @$arr, 6, 1 );
	splice( @$arr, 7, 1 );

	print "StatStruct: Trimed to :" . (join ',', @$arr ) . "\n";

	$self->[0] = $arr;

	return $self;
}

sub serialize
{
	my $self = shift; 

	my $str = join ',', @{$self->[0]};

	debug( "Serialized to: $str");

	return $str;
}

sub deserialize
{
	my $self = shift; 
	my $str = shift;

	die unless defined $str;
	
	debug( "Deserialized from: [$str]");

	my @arr = split ',', $str;
	
	$self->[0] = \@arr;

	return $str;
}

sub isSameAs
{
	my $self = shift;
	my $other = shift;

	die unless ref $self;
	die unless ref $other;

	my $result = 0;

	if( 0 ) # $self == $other)
	{
		$result = 1;
	}
	else
	{
		$result = ($self->serialize() eq $other->serialize() ) ? 1 : 0;
	}

	debug "isSameAs: $self vs $other = $result\n";

	return $result; 
}

sub hasMeaningfullyChanged
{
	my $self = shift;
	my $other = shift;

	# ($dev,$ino,$nlink,$uid,$gid,$rdev,$atime,$ctime,$blksize,$blocks)

	return 1 if $self->getInode() != $other->getInode();
	return 1 if $self->getCtime() != $other->getCtime();

	return 0;
}


sub getInode
{
	my $self = shift;
	return $self->[0]->[1];
}

sub getCtime
{
	my $self = shift;
	return $self->[0]->[7];
}


# GhostStateTransform Class
#
# Holds the state of a transitional mapping between the current state of the
# world and a desired final state. Provides a lot of tools for manipulating
# this final state and then ultimately checking that it's performed.
#
# The caller starts by informing the object of the LFNs that should be state
# managed. GST will hold onto the original LFNs with setOriginal(). The
# default mode is that these original files are presumed to be the initial
# state. The user can also specify (if they want) a final state.
#
# At this point, move() or copy() commands can be used inside the GST. The GST
# checks to be sure there is coherence between the action and the emerging
# state of the filesystem. There are functions to automatically generate these
# according to certain rules so that the final state is reached.
#
# The GST is done with the currentState = finalState, and a set of work
# instructions are complete. CurrentState and finalStates typically are
# invented LFNs.





package GhostStateTransform;

use base qw(Class::Accessor);
GhostStateTransform->mk_accessors( qw( _lfnState orphanGS) );


sub CODEPATH($)
{	
	my $p = shift; 
	main::CODEPATH( "GST: $p" );
}

sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();

	$self->_lfnState( {} );
	$self->orphanGS( GhostStore->new() );

	return $self;
}

sub _setOriginal
{
	my $self = shift;
	my $lfn = shift;
	my $ghost = shift;

	my $lfnState = $self->_lfnState();
	$lfnState->{$lfn}->[0] = $ghost; 
}

sub _setCurrent
{
	my $self = shift;
	my $lfn = shift;
	my $ghost = shift;

	my $lfnState = $self->_lfnState();
	$lfnState->{$lfn}->[1] = $ghost; 

	# $self->{_curGhostToPath}->{$ghost} = $lfn;
}

sub _setFinal
{
	my $self = shift;
	my $lfn = shift;
	my $ghost = shift;

	my $lfnState = $self->_lfnState();
	$lfnState->{$lfn}->[2] = $ghost; 
}


sub _getOriginal
{
	my $self = shift;
	my $lfn = shift;

	my $lfnState = $self->_lfnState();
	return $lfnState->{$lfn}->[0];
}


sub _getCurrent
{
	my $self = shift;
	my $lfn = shift;

	my $lfnState = $self->_lfnState();
	return $lfnState->{$lfn}->[1];
}


sub _getFinal
{
	my $self = shift;
	my $lfn = shift;

	my $lfnState = $self->_lfnState();
	return $lfnState->{$lfn}->[2];
}


sub setOriginal
{
	my $self = shift;
	my $g = shift;
	
	$self->_setOriginal( $g->lfn(), $g );
	$self->_setCurrent( $g->lfn(), $g );
}

sub setTargetState
{
	my $self = shift;
	my $lfn = shift;
	my $g = shift;

	# my $c = $g->cloneWithLFN( $lfn );

	$self->_setFinal( $lfn, $g );
}



sub LFNtest_isComplete()
{
	my $self = shift;
	my $lfn = shift;
		my $lfnState = $self->_lfnState();
	my ( $original, $current, $final ) = @{$lfnState->{$lfn}};
	return 1 if defined $current and defined $final and $current->isDuplicateOf($final);
	return 0; 
}


sub LFNtest_isMissing()
{
	my $self = shift;
	my $lfn = shift;
		my $lfnState = $self->_lfnState();
	my ( $original, $current, $final ) = @{$lfnState->{$lfn}};
	return 1 if !defined $current and defined $final;
	return 0; 

}

sub LFNtest_isSpurious()
{
	my $self = shift;
	my $lfn = shift;
		my $lfnState = $self->_lfnState();
	my ( $original, $current, $final ) = @{$lfnState->{$lfn}};
	return 1 if defined $current and !defined $final;
	return 0; 

}

sub LFNtest_isIncorrect()
{
	my $self = shift;
	my $lfn = shift;
		my $lfnState = $self->_lfnState();
	my ( $original, $current, $final ) = @{$lfnState->{$lfn}};
	return 1 if defined $current and defined $final and !$current->isDuplicateOf($final);
	return 0;
}


sub generateOrphanLFN
{
	my $self = shift;
	my $g = shift;

	# sort of cloned-ish code , maybe unite with generateAlternativeLocation

	$g->getPath(); 
	my $tk = TruthKeeper::get();

	my $newOrphanNameFound = 0;
	my $scramble = 0;
	my $finalNewOrphanLFN = 0; 

	while( not $newOrphanNameFound )
	{
		$scramble++;
		# Calculate a new name for this file
		my $orphanNewPathName = $g->getPath() . "-ORPH-$scramble";
		my $orphanNewLFN = main::assembleLFN( $g->getHandler() , $g->getRoot(), $orphanNewPathName );
		my $orphanGhost = $tk->obtainGhostForLFN( $orphanNewLFN );
		print "       - testing orphan: $orphanNewLFN\n";
		next if $orphanGhost->exists();
		next if defined $self->_getCurrent( $orphanNewLFN );
		next if defined $self->_getFinal( $orphanNewLFN );
		$finalNewOrphanLFN = $orphanNewLFN;
		$newOrphanNameFound = 1;
	}

	return $finalNewOrphanLFN;
}

sub doforeachLFN($&)
{	
	my $self = shift;
	my $function = shift;

	my $entry = 


	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		$function->($self,$lfn);
	}

}

sub printDebug
{
	my $self = shift;

	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		print $self->debugStringForLFN( $lfn,0 )."\n";
	}
}

sub printDebugWithPaths
{
	my $self = shift;

	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		print $self->debugStringForLFN( $lfn,1)."\n";
	}
}

sub debugStringForLFN
{
	my $self= shift;
	my $lfn = shift;
	my $pathmode = shift;

	my $lfnState = $self->_lfnState();
	my ( $original, $current, $final ) = @{$lfnState->{$lfn}};

	if( $pathmode )
	{
	$original = $original->lfn() if ref $original;
	$current = $current->lfn() if ref $current;
	$final = $final->lfn() if ref $final;
}
else
{
	$original = $original->debugID() if ref $original;
	$current = $current->debugID() if ref $current;
	$final = $final->debugID() if ref $final;
}

	$original ||= "<none>";
	$current  ||= "<none>";
	$final    ||= "<none>";



	# return "$original\t$current\t$final\t$lfn";
	return sprintf( "%-35s %-35s %-35s %s", $original, $current, $final, $lfn );
}


sub createClearanceMoves
{
	my $self = shift;

	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		my ( $original, $current, $final ) = @{$lfnState->{$lfn}};

		print $self->debugStringForLFN( $lfn )."\n";
		
		if( defined $current and defined $final and !$current->isDuplicateOf($final) )
		{		
			my $orphanLFN = $self->generateOrphanLFN( $current );
			print "Clearance $lfn -> $orphanLFN \n";

			my $orphan = $self->moveLFN( $lfn, $orphanLFN );

			#print "   Current = ".$current->makeDebugString()."\n";
			#print "   Orphan = ".$orphan->makeDebugString()."\n";

			my $ogs = $self->orphanGS();
			# print "OGS = $ogs\n";
			$ogs->addGhost( $current );
		}
	}
}

sub moveLFN
{
	my $self = shift;
	my $from = shift;
	my $to = shift;

	$self->addWorklist( -action=>'MOVE', -source=>$from, -destination=>$to, -debug=>"X" );
	my $g = $self->_getCurrent($from);
	my $o = $self->_getCurrent($to);
	die if defined $o; 
	$self->_setCurrent($to, $g);
	$self->_setCurrent($from, undef );
}

sub copyLFN
{
	my $self = shift;
	my $from = shift;
	my $to = shift;

	$self->addWorklist( -action=>'COPY', -source=>$from, -destination=>$to, -debug=>"X" );
	my $g = $self->_getCurrent($from);
	my $o = $self->_getCurrent($to);
	die if defined $o; 
	$self->_setCurrent($to, $g);
}

sub haulGhostToLFN
{
	my $self = shift;
	my $g = shift;
	my $to = shift;


	my $from = $g->lfn();

	$self->addWorklist( -action=>'HAUL', -source=>$from, -destination=>$to, -debug=>"X" );
	my $o = $self->_getCurrent($to);
	die if defined $o; 
	$self->_setCurrent($to, $g);
}

sub obtainCurrentStateAsGS
{
	my $self = shift;

	my $gs = GhostStore->new();

	my $lfnState = $self->_lfnState();

	# current states may have LFNs that appear more than once 
	# say a copy, or somesuch. Ghoststores don't like that.

	my %seen;

	foreach my $lfn ( sort keys %$lfnState )
	{
		my ( $original, $current, $final ) = @{$lfnState->{$lfn}};
		next if not defined $current; 
		next if $seen{$current->lfn()}++;
		$gs->addGhost( $current );
	}

	return $gs;
}


sub handlePillageFromGS
{
	my $self = shift;
	my $gs = shift;

	print "My GS = $gs\n";
	#$gs->printDebugShort();
	$gs->printDebugMedium();

	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		my ( $original, $current, $final ) = @{$lfnState->{$lfn}};

		print "HandlePillageGS: GST=".$self->debugStringForLFN( $lfn )."\n";

		## Bug - will keep doing this over and over again, gotta stop it
		# from moving files more than once.

		if( defined $final and !defined $current)
		{
			print "    - Check for pillage!\n";
			if( my $pillage = $gs->findSingleDuplicate($final)  )
			{
				print " - Pillage found..[$pillage]\n";
				my $curLFN = $self->findOneInProgressLFNforGhost( $pillage );
				my $n = $self->moveLFN( $curLFN, $lfn);
			}
		}
	}
}

sub findOneInProgressLFNforGhost
{
	my $self = shift;
	my $g = shift;

	my $lfnState = $self->_lfnState();
	foreach my $lfn (  keys %$lfnState )
	{
		CODEPATH( "Horrors");
		return $lfn if $self->_getCurrent( $lfn ) == $g;
	}

	return undef; 
}

sub handleLocalCopyFromGS
{
	my $self = shift;
	my $gs = shift;

	#print "My GS = $gs\n";
	#$gs->printDebugShort();
	#$gs->printDebugMedium();

	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		my ( $original, $current, $final ) = @{$lfnState->{$lfn}};

		print "handleLocalCopyFromGS: GST=".$self->debugStringForLFN( $lfn )."\n";

		if( defined $final and !defined $current)
		{
			print "    - Check for localcopy!\n";
			if( my $hit = $gs->findSingleDuplicateIncludingSelf($final)  )
			{
				print " - Localcopy found..[$hit]\n";
				my $curLFN = $self->findOneInProgressLFNforGhost( $hit );
				# my $n = $self->copyGhostToLFN( $hit, $final->lfn());

				my $n = $self->copyLFN( $curLFN, $lfn);

			}
		}
	}
}


sub verifyCorrectness
{
	my $self = shift;

	my $lfnState = $self->_lfnState();

	foreach my $lfn ( sort keys %$lfnState )
	{
		my ( $original, $current, $final ) = @{$lfnState->{$lfn}};

		print "Verify: GST=".$self->debugStringForLFN( $lfn )."\n";

		if( defined $final and !defined $current)
		{
			die "Position not defined: $lfn";
		}
		if( defined $final )
		{
		die "Hash mismatch @ $lfn"  unless $final->isDuplicateOf($current);
	}
	}

}


sub handleLongHaulCopyFromGS
{
	my $self = shift;
	my $gs = shift;

	#print "My GS = $gs\n";
	#$gs->printDebugShort();
	#$gs->printDebugMedium();

	my $transferGS = GhostStore->new();

	my $lfnState = $self->_lfnState();
	foreach my $lfn ( sort keys %$lfnState )
	{
		my ( $original, $current, $final ) = @{$lfnState->{$lfn}};

		print "handleLongHaulCopyFromGS: GST=".$self->debugStringForLFN( $lfn )."\n";

		if( defined $final and !defined $current)
		{
			print "    - Check for haul [$lfn]!\n";

			if( my $alreadyTransfered = $transferGS->findSingleDuplicateIncludingSelf( $final ))
			{
				print "    - Already transfered [$lfn]!\n";
				next;
			}

			if( my $hit = $gs->findSingleDuplicateIncludingSelf($final)  )
			{
				print " - Haul found..[$hit]\n";

				my $n = $self->haulGhostToLFN( $hit, $lfn);
				$transferGS->addGhost($n);

			}
		}
	}
}


sub addWorklist
{
	my $self = shift;
	my %hash = @_;
	my $worklist = 

	my $action = $hash{-action};

	my $S = $hash{-source};
	my $D = $hash{-destination};

	print "Adding Worklist: $action [$S] [$D]\n";

	# foreach my $k (sort keys %hash )
	# {
	# 	print "     $k => $hash{$k}\n";
	# }

	push @{$self->{WL}}, \%hash;
}


sub printWorklist
{
	my $self = shift;
	my $worklist = $self->{WL};
	foreach my $hash (@$worklist)
	{
			my $action = $hash->{-action};

	my $S = $hash->{-source};
	my $D = $hash->{-destination};

	print "$action $S $D\n";


	}
}


package FilesystemExplorer::ObjectStore;

use File::Basename;
use File::Spec; 
use File::Spec::Functions;
use File::Path; 
use base qw(Class::Accessor);
FilesystemExplorer::ObjectStore->mk_accessors( qw( _rootDir _isInitialized ) );

sub debug($)
{
	my $a = shift;
	main::debug( "fse/OJX: " . $a );
}

sub new
{
	my $class = shift;
	my $root = shift;
	my $self = $class->SUPER::new();

	die unless defined $root; 
	$self->_rootDir( $root );
	$self->_isInitialized( 0 );

	return $self;
}

sub finalize
{
	my $self = shift;

	# in practice, this is not always called. It may not have entered the FSE
	# cache. i'm not sure yet if that is an issue or not.

	debug "Shutdown $self.. - no action taken." if $fseOjxDebug;
}

sub initialize
{
	my $self = shift;

	$self->{ROOTDIR} = $self->_rootDir();
	$self->{PTR_DIR} = catfile( $self->_rootDir(), 'ptr' );
	$self->{OBJ_DIR} = catfile( $self->_rootDir(), 'obj' );

	die unless -w $self->{ROOTDIR};
	die unless -w $self->{PTR_DIR};
	die unless -w $self->{OBJ_DIR};

	$self->_isInitialized(1);
}

sub setUpRepository
{
	my $self = shift;

	$self->internalMkDir( catfile( $self->_rootDir() ));
	$self->internalMkDir( catfile( $self->_rootDir(), 'ptr' ));
	$self->internalMkDir( catfile( $self->_rootDir(), 'obj' ));

	$self->initialize();
}

sub internalMkDir
{
	my $self = shift;
	my $p = shift;

	main::globalMkPath( $p );

	# main::mkdirs( File::Spec->catfile( $self->{ROOTDIR}, "foo" ));
}

sub setPointer
{
	my $self = shift;
	my $name = shift;
	my $value = shift;

	die unless $self->_isInitialized();
	die "Pointers cannot be empty strings" if length($name) < 1;
	die "Pointers cannot have trailing slashes" if $name =~ /\/$/;

	debug( "setPtr: [$name]=>[$value]") if $fseOjxDebug;

	$self->{PTR}->{$name} = $value;

	my $targetPath = catfile( $self->{PTR_DIR}, ($name . ".ptr" ));

	die "setPointer tried to replace directory with a file" if( -d $targetPath );

	# Super schitzophrenic - shou;dn't this ALL be done via the ghost API?
	my $dirPath = dirname $targetPath;
	debug( "Checking readibility of [$dirPath]");
	if( ! -e $dirPath )
	{
		$self->internalMkDir( $dirPath );
	}

	open FILE, ">$targetPath" or die "Could not open [$targetPath] for pointer: $!";
	print FILE $value;
	close FILE,
}

sub getPointer
{
	my $self = shift;
	my $name = shift;
	
	die unless $self->_isInitialized();
	die "Pointers cannot have trailing slashes" if $name =~ /\/$/;
	die "Pointers cannot be empty strings" if length($name) < 1;

	my $targetPath = catfile( $self->{PTR_DIR}, $name );

	if( exists $self->{PTR}->{$name})
	{
		my $value = $self->{PTR}->{$name};
		debug( "getPtr: [$name]=>[$value] (cache)") if $fseOjxDebug;
		return $value;
	}

	if( ! -e $targetPath )
	{
		my $value = undef; 
		debug( "getPtr: [$name]=>[undef] (stat)") if $fseOjxDebug;
		return $value;
	}

	local $/ = undef;
	local *FILE;
	open FILE, "<$targetPath" or die "Could not open [$targetPath] for pointer: $!";
	my $value = <FILE>;
	close FILE,

	$self->{PTR}->{$name} = $value;
	debug( "getPtr: [$name]=>[$value] (disk)") if $fseOjxDebug;

	return $value; 
}


# Provides a universal name for the LFN that should be completely invarient to
# CWD, time and space, etc. Basically, this ghost should be the same no matter
# where in the cluster of machines the user is running it.
# 
# CORE

sub getAbsoluteLFNForGhost
{
	my $self = shift;
	my $g = shift;

	# By design, object store references are already universal

	return $g->lfn();
}


# One of the universal methods. It basically says make a copy of the things
# I'm pointing to and gives you the name of the place to store it. If there is
# something already in that position, leaves it up to each FSE what to do with it.

sub store
{
	my $self = shift;
	my $source = shift;
	my $target = shift;

	die "file to store does not exist" unless $source->exists();
	die "store only works for single files" unless $source->isSingleFile();

	# my $hardFilename = $source->getHardFile();

	my $hash = $source->getFullHash();
	my $justHash = main::extractHashComponents( $hash )->[2];

	my $targetPath = catfile( $self->{OBJ_DIR}, $justHash );

	# Super schitzophrenic - shou;dn't this ALL be done via the ghost API?
	$self->internalMkDir( $targetPath );

	my $finalPath = catfile( $targetPath, $justHash );

	my $targetLFN = main::assembleLFN( "fs", "root", $finalPath);
	my $tk = TruthKeeper::get();
	my $targetGhost = $tk->obtainGhostForLFN($targetLFN);
	$targetGhost->store( $source );

	my $targetUserPath = $target->getPath();

	if( length($targetUserPath) > 0 )
	{
		my $pointerName = catfile( "adhoc", $targetUserPath );
		$self->setPointer( $pointerName, $hash );
	}
}


# A universal method that says "take the things I pointed to, and make sure
# you've stored it somewhere". In this form, you're not allowed to say where
# it gets stored or how, just that it shpuld be there.

sub absorb 
{
	my $self = shift;
	my $source = shift;
	die "absorb only works for single files" unless $source->isSingleFile();

	die "absorb not implemented for this FSE type.";
}


# Core 
#
# Forces the ghost, if it exists, to return an absolute file on the local
# filesystem. This should ALWAYS return a local path (can be a temp file.) the
# caller must promise to not tamper with this in any way other than a read.

sub getReadonlyHardFile
{
	my $self = shift;
	my $g = shift;

	return $self->getHardFilePathForGhost( $g );
}

# give a OBJ: style ghost and get a ghost pointing to the underlying file

sub getHardFileGhost
{
	my $self = shift;
	my $g = shift;

			my $tk = TruthKeeper::get();
			my $targetLFN = $self->getHardFileFQLFNforGhost( $g );
	my $targetGhost = $tk->obtainGhostForLFN($targetLFN);
	return $targetGhost; 
}


# Give a OBJ: type ghost and get the hard file local filename corresponding to it

sub getHardFileFQLFNforGhost
{
	my $self = shift;
	my $g = shift;


	my $finalPath = $self->getHardFilePathForGhost( $g );
	my $targetLFN = main::assembleLFN( "fs", "root", $finalPath);

	return $targetLFN;
}

sub getHardFilePathForGhost
{
	my $self = shift;
	my $g = shift;

	my $path = $g->getPath();
	my $pointerName = catfile( "adhoc", $path ) . ".ptr";
	my $hash = $self->getPointer( $pointerName );
	my $justHash = main::extractHashComponents( $hash )->[2];
	my $targetPath = catfile( $self->{OBJ_DIR}, $justHash );
	my $finalPath = catfile( $targetPath, $justHash );

	return $finalPath; 
}


sub getStatSourceVerify
{
	my $self = shift;
	my $g = shift;
	my $time = shift;
	
	my $hardfile = $self->getHardFileGhost( $g );
	return( $hardfile->getStatSourceVerify( $time ) );
}


sub getFullHashSourceVerify
{
	my $self = shift;
	my $g = shift;
	my $time = shift;
	
	my $hardfile = $self->getHardFileGhost( $g );
	return( $hardfile->getFullHashSourceVerify( $time ) );
}



package FilesystemExplorer::Snapshot;

use File::Basename;
use base qw(Class::Accessor);
FilesystemExplorer::Snapshot->mk_accessors( qw( dirty rootGhost rootGhostStore isFinalized deNovo) );


sub debug($)
{
	my $a = shift;
	main::debug( "FSE/ss: " . $a );
}

sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();

	return $self;
}


sub getAbsoluteLFNForGhost
{
	my $self = shift;
	my $g = shift;

	# The correct implementation would be to get the unique hash for this
	# snapshot, whatever it is in the "root" component of the LFN. That unique
	# hash code may change over time, so we are going to impose a rule that
	# you can  only ask this kind of question if the snapshot is not dirty.
	# Our callers will have to tolerate this

	die "Absolute LFNs do not exist for snapshots while in a dirty state" if $self->dirty();

	return main::rewriteLFNreplaceRoot( $g->lfn(), "HACKED_HASH_GOES_HERE");
}


sub absorb
{
	my $self = shift;
	my $g = shift;

	my $DID = $g->debugID();

	debug "Storing ghost $DID" if $gsDebug;

	# FOR NOW, we'll assume that snapshots are pretty immuteable, 
	# but clearly there is no reason for them to be so

	# die if defined $self->rootGhostStore();


	my( $handler, $root, $path ) = main::parseFQLFN( $g->lfn() );

	$handler = "snap";
	$root = "root";

	my $newLFN = main::assembleLFN( $handler, $root, $path );

	my $copy = $g->cloneWithLFN( $newLFN );
	# TODO not clear if this is a boundary violation SOC issue
	$copy->_immutable(1);



	# this is definitely a truthy thing
	# TODO boundary issue, what if user's new name for the snapshot overwrites an existing 
	# snapshot name??? what is "correct"
	TruthKeeper::get()->registerTruthyGhost( $copy );

	$self->rootGhostStore()->addGhost( $copy );

	return $copy; 
}


sub finalize
{
	my $self = shift;

	return if $self->isFinalized();

	my $store = $self->rootGhostStore();

	$self->loadRootIfNeeded();

	# The rule for now is a snapshot FSE is not changed after its opened If
	# you want to do that, you'd clone it to a new object So the FSE is either
	# in a read state (deNovo = false) or in a making a new thing state
	# (deNovo = TRUE).

	if( not $self->deNovo() )
	{
		debug "Snapshot is not new, no finalization performed." if $fseSnapshotDebug;
	}
	else
	{
		# TODO HACK
		# technically should delegate to the FSE controller
		$store->writeSnapshotToDisk( $self->rootGhost()->getRoot() );

		foreach my $g ( $store->getAllGhosts() )
		{
			$g->dirty(0);
		}

		$self->dirty(0);
	}

	$self->isFinalized(1);
}



# I'm still working out the right meaning exactly, but when you ask a FSE for
# a ghost like this, I think it's supposed to mean you want one that is fully
# truth enabled, not just any crappy ghost. we may need to rename this
# function once we know how the semantic should be honored

sub findGhostForLFN
{
	my $self = shift;
	my $lfn = shift;

	$self->loadRootIfNeeded();

	return $self->rootGhostStore()->findGhostForLFN( $lfn ); 

}

sub loadRootIfNeeded
{
	my $self = shift;

	my $rgs = $self->rootGhostStore();

	return $rgs if ref $rgs;

	$rgs = GhostStore->new();
	$self->rootGhostStore( $rgs );

	# Hack, probably should handle this with a FS ghost
	if( -r $self->rootGhost()->getRoot()  )
	{
		$rgs->slurpSnapshot( $self->rootGhost()->getRoot(), $self->rootGhost()->getRoot() );
		# dirty flag is clean because we are consistent with source backed file
		$self->dirty(0);
		$self->deNovo(0);
	}
	else
	{
		warn "In that unusual case where we don't load anything";
		# dirty flag is TRUE because we are in an uncommitted state
		$self->dirty(1);
		$self->deNovo(1);
	}

#	debug "Incorporating snapshot truth\n" if $tkdebug;

	TruthKeeper::get()->incorporateGhostStore( $rgs );



	return $rgs;
}

sub getFullHashSourceVerify
{
	my $self = shift;
	my $lfn = shift;

	die "getFullHashSourceVerify - Snapshots are immutable - should not have reached this point\n";

	# my $rgs = $self->loadRootIfNeeded();

	# return ($size, $mtime, $mode);
}

sub getStatSourceVerify
{
	my $self = shift;
	my $lfn = shift;

	die "getStatSourceVerify - Snapshots are immutable - should not have reached this point\n";


	# my $rgs = $self->loadRootIfNeeded();

	# my ($size, $mtime, $mode);

	# return ($size, $mtime, $mode);
}


##########
# The FSE class handles low level access and coherency for a particular
# domain of listed files. This could be a real file system or a snapshot
# or even a remote filesystem or S3 type thing.
# 
# I think this is the right definition for the class: 
#
# It provides the ability to iterate over the filesystem in an organized way.
# Everything that hits a primary resource should go here
# FSE is mainly called by the ghost API
# 
# Verbs i know it has to support
#
# - iteration
# - findDuplicates();
# - forceFullCharacterization();

package FilesystemExplorer::FS;

use Time::HiRes qw( CLOCK_REALTIME gettimeofday tv_interval);
use File::Basename;
use File::Copy;
use base qw(Class::Accessor);
FilesystemExplorer::FS->mk_accessors( qw(topLevelLFNs sizeToFNs) );

# Return new FSE
# CORE - any FSE must implement


sub debug($)
{
	my $a = shift;
	main::debug( "FSE/fs: " . $a );
}

sub CODEPATH($)
{	
	my $p = shift; 
	main::CODEPATH( "FSE/fs: $p" );
}

sub finalize
{
	my $self = shift;

	# in practice, this is not always called. It may not have entered the FSE
	# cache. i'm not sure yet if that is an issue or not.

	debug "Shutdown $self.. - no action taken." if $fseDebug;
}

sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();

	$self->sizeToFNs( undef );

	return $self;
}

# CORE

sub getAbsoluteLFNForGhost
{
	my $self = shift;
	my $g = shift;

	return main::rewriteLFNPathAbsolute($g->lfn());
}

# CORE

sub getFullHashSourceVerify
{
	my $self = shift;
	my $g = shift;
	my $time = shift; 

	my $path = $g->getPath();

	print "*** FULLHASH: $path\n";

	# TODO: examine use of stat
	$main::run_bytes_fullread += -s $path;

	my $START_GTOD = [gettimeofday];

	my $sha = Digest::SHA->new(1);
	$sha->addfile( $path );
	my $hexdigest = $sha->hexdigest();

	$main::run_files_fullread++;

	$main::run_time_fullread += tv_interval( $START_GTOD );

	return( "SHA1/FULL/$hexdigest");
}

# CORE

sub getStatSourceVerify
{
	my $self = shift;
	my $g = shift;
	my $time = shift;


	my $path = $g->getPath();

	print "*** STAT: $path\n";

	$main::run_files_stat++;

	my @statfields = stat($path);

	if( @statfields == 0 )
	{
		my $errcode = $! + 0;
		print "DEBUG: $! = $errcode\n";
		return (undef, undef, main::statModeGen_error($errcode));
	}

	# die "Ghost [$path] failed stat - probably doesn't exist: $! ($@)" ;
	my ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,$atime,$mtime,$ctime,$blksize,$blocks) = @statfields; 
	
	my $statStruct = new StatStruct;
	$statStruct->loadFromPOSIXstat( \@statfields );

	return ($size, $mtime, $mode, $statStruct );
}



# Return LFNs of immediate children
# CORE - any FSE must implement

sub getImmediateChildren
{
	my $self = shift;
	my $parent = shift;

	my $dir = $parent->getPath();

	print "*** Scandir [$dir]\n";

	print "FSE:ScanDIR into dir:[$dir]\n" if $fseDebug;

	#$dir =~ s/\/+$//;

	opendir DIR, $dir or die "cannot descend into [$dir] for getImmediateChildren: $! $@";

	$main::readdir_invokes++;

	# drop . and .., sort directory
	my @files = map { "fs:root:" . $_ }
	    map {  main::canonize( File::Spec->catfile( $dir, $_ ) ) } 
		grep { !/^\.{1,2}$/ } 
		grep { !main::isHardIgnore( $_ ) } 
		sort { $a cmp $b } 
		readdir DIR;

	$main::readdir_entries += @files;

	print( "FSE:ScanDIR Returned: [", (join '],[', @files ), "]\n" ) if $fseDebug;

	close DIR;

	return @files;
}

# 
#
# CORE
# 
# One of the universal FSE methods. It basically says make a copy of the things
# I'm pointing to and gives you the name of the place to store it. If there is
# something already in that position, leaves it up to each FSE what to do with it.

# BE CAREFUL: We are always the FSE handler for the TARGET, not the SOURCE, so
# our first job is to force the the SOURCE to something we're prepared to
# read.

sub store
{
	my $self = shift;
	my $sourceGhost = shift;
	my $target = shift;

	die "file to store does not exist" unless $sourceGhost->exists();
	die "store only works for single files" unless $sourceGhost->isSingleFile();

	# this extra overhead forces the file to be a filesystem object and
	# ensures that we are only working with a ghost that is authoritative.

	my $hardFilename = $sourceGhost->getReadonlyHardFile();
	my $sourceFileLFN = main::assembleLFN( "fs", "root", $hardFilename);
	my $tk = TruthKeeper::get();
	my $sourceFileGhost = $tk->obtainGhostForLFN($sourceFileLFN);

	my $srcPath = $sourceFileGhost->getPath();
	my $destPath = $target->getPath();

	print "store() - COPY - [$srcPath]->[$destPath]\n";

	my $finalTarget; 

	if( $target->exists( time() ) )
	{
		if( $target->isDuplicateOf($sourceGhost) )
		{
			print "store(): Source and destination are identical, no action.\n";
			CODEPATH( "store.identical");
			return $target; 
		}
		else
		{
			print "store(): destination exists, generating new location.\n";
			my $newLocation = $target->generateAlternativeLocation( 'storeOperation' );
			CODEPATH( "store.relocate");

			# should be a move, not a copy, but that means we have to work out move semantics
			$newLocation->store($target);
			$finalTarget = $target;
		}
	}
	else
	{
		$finalTarget = $target;
	}

	my $finalPath = $finalTarget->getPath();

	print "store(): Invoking full copy [$srcPath]->[$finalPath]\n";

	my $sourceSize = $sourceGhost->getSize();

	 $main::run_files_copied++;
	 $main::run_bytes_copied += $sourceSize;

	my $r = copy( $srcPath, $finalPath );

	if( !$r )
	{
		die "Copy operation failed - [$srcPath]->[$finalPath]: err: $!";
	}

	$finalTarget->learnContentsChanged( time() );

	# Perform a quick verification that the file sizes match. Not strictly
	# needed. The caller can force more verification if it wants.

	if( $finalTarget->getSize() != $sourceSize )
	{
		die "Assert: My copy succeeded but the getSize() no longer matches - what gives?";
	}

	return $finalTarget; 

	# die "Copy not implemented";

}


# 
#
# CORE  
# 
# One of the universal FSE methods. When this is invoked, we are always
# the FSE manager for the SOURCE. (This is the opposite of the case of a copy.)
# This should die if the move cannot be performed in the way that preserves everything. 

sub moveGhost
{
	my $self = shift;
	my $source = shift;
	my $target = shift;


	die "file to move does not exist" unless $source->exists();

	die "store only works for single files" unless $source->isSingleFile();
	die "store only works for single files" unless $target->isSingleFile();

	my $srcPath = $source->getPath();
	my $destPath = $target->getPath();

	die unless $source->getHandler() eq 'fs';
	die unless $target->getHandler() eq 'fs';

	print "move() - MOVE - [$srcPath]->[$destPath]\n";

	die "move() - already a file present at target location!" if $target->exists( time() );

	my $originalSize = $source->getSize( time() );

	$main::run_files_moved++;
	my $r = move( $srcPath, $destPath );

	if( !$r )
	{
		die "Copy operation failed - [$srcPath]->[$destPath]: err: $!";
	}

	my $effectiveTime = time() ;
	$source->learnContentsChanged( $effectiveTime );
	$target->learnContentsChanged( $effectiveTime );

	# Perform a quick verification that the file sizes match. Not strictly
	# needed. The caller can force more verification if it wants.

	my $newSize = $target->getSize( $effectiveTime );

	if( $newSize != $originalSize  )
	{
		die "Assert: Move succeeded but the getSize() no longer matches - what gives? Orig=$originalSize New=$newSize";
	}


	return $target;
}


# Returns a new available location for a file that is writeable and guarenteed
# to not be re-used

local %FilesystemExplorer::FS::altLocationGuarentee;

sub generateAlternativeLocation
{
	my $self = shift;
	my $g = shift;
	my $strategy = shift; 

	$g->getPath(); 
	my $tk = TruthKeeper::get();

	my $newOrphanNameFound = 0;
	my $scramble = 0;
	my $finalNewOrphanLFN = 0; 

	my $datestr = POSIX::strftime( "%Y%m%d", gmtime() );

	while( not $newOrphanNameFound )
	{
		# Calculate a new name for this file
		my $orphanNewPathName = $g->getPath() . ".gsfmoved-$datestr";
		$orphanNewPathName .= "-$scramble" if $scramble > 0;
		my $orphanNewLFN = main::assembleLFN( $g->getHandler() , $g->getRoot(), $orphanNewPathName );
		my $orphanGhost = $tk->obtainGhostForLFN( $orphanNewLFN );
		print "       - testing orphan: $orphanNewLFN\n";
		$scramble++;
		if( $orphanGhost->exists() )
		{
			CODEPATH( "Rare - orphanNewLFN - needed additional clearance check" );
			next;
		}
		if( $FilesystemExplorer::FS::altLocationGuarentee{orphanNewLFN} )
		{
			CODEPATH( "Rare - orphanNewLFN - guarnetee override" );
			next; 
		}

		$FilesystemExplorer::FS::altLocationGuarentee{$orphanNewLFN} = 1; 
		return $orphanGhost;
	}

	die "Logic error.";
}


# Forces the ghost, if it exists, to return an absolute file on the local
# filesystem. This should ALWAYS return a local path (can be a temp file.) the
# caller must promise to not tamper with this in any way other than a read.

sub getReadonlyHardFile
{
	my $self = shift;
	my $thing = shift; 

	# Implementation is super easy - just get my path and be done!

	die unless $thing->exists();

	return $thing->getPath();
	
}





package FSEFactory;

# is container the right term for this?

my $fseFactoryDebug = 0;
my %fseForHandlerRoot;

sub debug($)
{
	my $a = shift;
	main::debug( "FSE-factory: " . $a );
}

sub shutdown
{
	my $self = shift;

	# TODO - odd exception if the local FSE handler is not here, right?

	debug( "-- Shutting down FSE Factory") if $fseFactoryDebug;

	foreach my $key ( keys %fseForHandlerRoot )
	{
		my $fse = $fseForHandlerRoot{$key};
		debug( "Sending finalization call to $fse") if $fseFactoryDebug;
		$fse->finalize();
	}
}



# This is the core function that dispatches out the FSE handler for the
# particular path. All handlers are ultimately registered here.

sub getFSEHandlerForGhost
{
	my $class = shift;
	my $g = shift;
	my $lfn = $g->lfn();

	# print "$lfn**\n";

	my $r = undef;

	my ($handler, $root, $path ) = main::parseFQLFN($lfn);

	my $key = $handler . ":" . $root;

	if( $handler eq 'snap' )
	{
		my $class = "FilesystemExplorer::Snapshot";

		## TODO - break this out into a cleaner function

		if( exists $fseForHandlerRoot{$key} )
		{
			debug "Found existing handler for $key @ $class"  if $fseFactoryDebug;
			$r = $fseForHandlerRoot{$key};
		}
		else
		{
			debug "Making new handler for $key @ $class"  if $fseFactoryDebug;
			$r = $class->new();
			$r->rootGhost($g);
		}
	}	
	elsif( $handler eq 'fs' )
	{
		die "FS handler for non-root is not supported yet" if $root ne 'root';
		$r = $main::localFileSystemFSE;
	}
	elsif( $handler eq 'obj' )
	{
		die "OBJ handler for non-root is not supported yet" if $root ne 'core';
		$r = $main::coreOjxFSE;
	}
	else
	{
		die "Unrecognized LFN handler: [$lfn] ERROR - Handler [$handler] is unknown.";
	}

	debug "FSEFactory: FSE handler for lfn:[$lfn] --> Class:[$r]" if $fseFactoryDebug;

	$fseForHandlerRoot{$key} = $r;

	return $r; 
}


package TruthKeeper;

use Data::Dumper;
use File::Basename;
use base qw(Class::Accessor);

my $global_tk;

my %ghostForLFN_private;
my %ghostForAFN_private;
my %ghostStoreForDescriptorLFN;

sub debug($)
{
	my $a = shift;
	main::debug( "TK: " . $a );
}


# Takes ghosts provided by a command line and  blows out a fully qualified LFN
# and gets the ghosts returns a nice structure of data you'd need for iteration
# on the command line: [provided ARGV, fully qualified ghost name, ghost]

sub makeGhostsFromCommandLine
{
	my $self = shift;
	my @stuff = @_;

	my $gs = GhostStore->newDescending();

	my @stash = ();

	foreach my $lfn (@stuff )
	{
		my $fqlfn = main::fullyQualifyLFN( $lfn );

		debug "Make Ghost Via CommandLine user provided ".
			"lfn:[$lfn]->fqLFN:[$fqlfn] from command line to store\n"
			if $tkdebug;

		my $g = $self->obtainGhostForLFN($fqlfn);
		$gs->addGhost( $g );
		push @stash, [$lfn, $fqlfn, $g ];
	}

	return ($gs, \@stash);
}

sub ghostForLFN
{
	my $self = shift;
	return \%ghostForLFN_private;
}

sub ghostForAFN
{
	my $self = shift;
	return \%ghostForAFN_private;
}

sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();
	# $self->ghostForLFN( +{} );
	return $self;
}

sub printInternallyTrackedLFN
{
	my $self = shift;
	my $ghostForLFN = $self->ghostForLFN();

	foreach my $g (values %$ghostForLFN )
	{
		debug ("     " 
			 . ($g->dirty() ? "YES" : "NO")
		     . "  "
		 	 . $g->lfn()
		     . "  " );

		 debug( "          -> ". $g->makeDebugString() );
	}
	return;
}

sub printInternallyTrackedAFN
{
	my $self = shift;
	my $ghostForAFN = $self->ghostForAFN();
	foreach my $g (values %$ghostForAFN )
	{
		debug ("     " 
			 . ($g->dirty() ? "YES" : "NO")
		     . "  "
		 	 . $g->absoluteLFN()
		     . "  " );

		 debug( "          -> ". $g->quickDebugString() );
	}
	return;
}


sub flushUniversalStore
{
	my $self = shift;
	my $ghostForLFN = $self->ghostForLFN();

	my $gdb = GhostDatabase->getCore();
	foreach my $g (values %$ghostForLFN )
	{
		 debug( "          -> ". $g->quickDebugString() );
		my $afn = $g->absoluteLFN();
		 debug( "             |->AFN: $afn");
	

		my $absGhost = $gdb->findGhostByAFN( $afn );
		$absGhost->learnFromGhost( $g );
 		$gdb->storeGhostToAFN( $g, $afn );

 	}
}






sub flush
{
	my $self = shift;

#	debug Dumper( $self );

	debug "---- Beginning flush ---\n" if $tkflushdebug;;

	debug "---- Inventory of all internally tracked ---\n" if $tkflushdebug;;

	$self->printInternallyTrackedLFN() if $tkflushdebug;

	debug "---- Inventory of all internally tracked AFNs ---\n" if $tkflushdebug;;

	$self->printInternallyTrackedAFN() if $tkflushdebug;

	debug "---- Ghost Cache Writes ---\n" if $tkflushdebug;

	$self->flushGhostDescriptorCache();

	debug "---- AFN ---\n" if $tkflushdebug;

	$self->flushUniversalStore();



	debug "---- Verify ---\n" if $tkflushdebug;

	$self->verifyNoDirtyGhostsRemain();
}

sub get
{
	my $class = shift;

	return $global_tk if defined $global_tk;

	$global_tk = new TruthKeeper();

	return $global_tk;
}

sub verifyNoDirtyGhostsRemain
{
	my $self = shift;
	my $ghostForLFN = $self->ghostForLFN();

	my $error = 0;

	foreach my $g (values %$ghostForLFN )
	{
		if( 0 )
		{
			debug "   Verifying TK Ghost: ";
			debug $g->printDebug();
			debug "  ";
			debug ($g->dirty() ? "YES" : "NO");
			debug "\n";
		}	

		# TODO - unresolved design decision, what do we do with dirty snapshot
		# stuff?

		if($g->dirty())
		{
			if( $g->getHandler() eq 'snap')
			{	
				my $lfn = $g->lfn();
				debug "Wierd straggler case - I haven't figured out yet who, if anyone flushes this type of object.: $lfn";
			}
			elsif( $g->getHandler() eq 'obj')
			{	
				my $lfn = $g->lfn();
				debug "Wierd straggler case - I haven't figured out yet who, if anyone flushes this type of object.: $lfn";
			}

			else
			{
				$error++;
			}
		}
	}

	die "Dirty ghosts remain unflushed!! assert" if $error > 0;
}

sub flushGhostDescriptorCache
{
	my $self = shift;

	foreach my $gslfn ( keys %ghostStoreForDescriptorLFN )
	{
		## my $gs = getGhostStoreForDescriptorFileCoveringFN( $k );

		$self->flushDescriptorToDisk( $gslfn );
	}
}

sub flushDescriptorToDisk
{
	my $self = shift;
	my $lfn = shift; # LFN of the ghoststore

	debug "TK: Flushing ghoststore [$lfn] to disk\n" if $tkdebug;

	if( not exists $ghostStoreForDescriptorLFN{$lfn} )
	{
		die "Internal assert - $lfn not found" if $tkdebug;
	}

	my $gs = $ghostStoreForDescriptorLFN{$lfn};

	$gs->purifyAllGhostsToTK();

	if( $gs->countDirty() > 0 )
	{
		$gs->writeAsDescriptor( $lfn );
	}
	else
	{
		debug "TK:   - Not needed, no dirty records\n" if $tkdebug;
	}

	map { $_->dirty(0) } $gs->getAllGhosts();

	delete $ghostStoreForDescriptorLFN{$lfn};

	return undef; 
}

sub getGhostDescriptorFileForLFN($)
{
	my $self = shift;
	my $filename = shift;

	# debug "GDF: $filename\n";

	die "Assert: $filename should be a path" if not main::assert_regularpath( $filename );

	my( $fn, $dir ) = fileparse( $filename );

 	my $dfn = File::Spec->catfile($dir, $DESCRIPTOR_FILENAME);

 	# debug "GDF [$dfn] for $filename\n";

	return $dfn;
}

sub getGhostStoreForDescriptorFileCoveringFN
{
	my $self = shift;
	my $lfn = shift;

	my $gsfn = $self->getGhostDescriptorFileForLFN( $lfn );

	if( exists $ghostStoreForDescriptorLFN{$gsfn} )
	{
		return( $ghostStoreForDescriptorLFN{$gsfn} );
	}
	else
	{
		# must revisit TODO - the point of this cache is that it shouldn't get too big
		# die "Overflow!" if ((keys %ghostStoreForDescriptorLFN) > 100);

		my $gs = GhostStore->new();

		if( -e $gsfn )
		{
			debug "Reading descriptor [$gsfn]\n" if $tkdebug;
			$gs->slurpDescriptor( $gsfn );
			debug "Incorporating stuff found in [$gsfn]\n" if $tkdebug;
			$self->incorporateGhostStore( $gs );

			# TODO
			# to properly handle identification of deleted files,
			# the ghosts should be added as children to whatever the
			# parent is
		}

		$ghostStoreForDescriptorLFN{$gsfn} = $gs;

		return $gs;
	}
}

# Takes everything we've learned from a ghost store and calls incorporate

sub incorporateGhostStore
{
	my $self = shift;
	my $gs = shift;

  # debug Dumper( $self );

	my @g = $gs->getAllGhosts();
	foreach my $g (@g)
	{
		$self->incorporateGhostData( $g );
	}

  # debug Dumper( $self );
}

# Takes everything we've learned from a ghost and incorporate it into our
# source of truth.  Ss a side effect, this may end up registering new ghosts
# as truthy. Either the ghost you provide is registered as the truthy ghost,
# or the existing one is updated.  Caller is always free to continue using
# whatever ghost they want but future lookups will always get a reference
# ghost with isTKVersion(1). If you want to be sure you are using that
# version, future use should be the TK version which is returns.

sub incorporateGhostData
{
	my $self = shift;
	my $g = shift;

	my $lfn = $g->lfn();
	my $ghostForLFN = $self->ghostForLFN();

	my $DID = $g->debugID();
	#debug "Incorporating ghost lfn:[$lfn] ptr:[$DID] (self=$self, glfn=$ghostForLFN)\n" if $icDebug;
	debug "Incorporating ghost lfn:[$lfn] ptr:[$DID]\n" if $icDebug;

	if( exists $ghostForLFN->{$g->lfn()} )
	{
		# merge case: My caller provided a ghost and I already have one in my registry

		my $base = $ghostForLFN->{$g->lfn()};
		if( $base == $g )
		{
			debug "  Incorporate: Asked to incorporate a ghost that is already truthy";
			die unless $base->isTKVersion() > 0;
			return;
		}

		# Now, presumably, I have a base that may need to be updated.

		$base->learnFromGhost( $g );

		# not super clear why this is here, maybe just affirmative?

		die "Assert: two ghosts authoritative to same LFN" if $g->isTKversion() > 0;

		$g->isTKversion(0);


		# my $base = $ghostForLFN->{$g->lfn()};

		# if( $base->lastFullHash() < $g->lastFullHash() )
		# {
		# 	die if $base->isImmutable();

		# 	$base->lastFullHash( $g->lastFullHash() );
		# 	$base->fullHash( $g->fullHash() );
		# 	$base->dirty(1);
		# 	debug "  Incorporate - Merger case - existing TK ghost updated (base=$base)\n" if $icDebug;
		# }
		# else
		# {
		# 	debug "  Incorporate -  Merger case - existing TK ghost was fine\n" if $icDebug;
		# }

		return $base; 
	}
	else
	{
		debug "  Incorporate -  Merger case - non-TK ghost we got is being declared the TK version\n" if $icDebug;

		$self->registerTruthyGhost( $g );
		#$ghostForLFN->{$g->lfn()} = $g;
		$g->isTKversion(1);
		return $g;
	}
}


sub registerTruthyGhost
{
	my $self = shift;
	my $g = shift;

	my $lfn = $g->lfn();

	my $DID = $g->debugID();
	debug "Register Truthy Thing: [$lfn] -> $DID\n" if $tkdebug;

	my $ghostForLFN = $self->ghostForLFN();
	die "Collision [$lfn]" if exists $ghostForLFN->{$lfn};
	$ghostForLFN->{$lfn} = $g;

	# ghosts that are not real should never be attempted to convert to AFNs? is
	# that true? they certainly shouldn't find their way into a persistent store.
	# if that were true, the truthkeeper has to learns that it is actualized, and
	# thats when it does and AFN test.

	# my $afn = $g->absoluteLFN();
	# my $ghostForAFN = $self->ghostForAFN();
	# die "Collision [$afn]" if exists $ghostForAFN->{$afn};
	# $ghostForAFN->{$afn} = $g;
}

# Normal logic to get a ghost -- first check if we have it already

sub obtainGhostForLFN
{
	my $self = shift;
	my $lfn = shift;

	my( $handler, $root, $path ) = main::parseFQLFN( $lfn );

	die "LFN is zero length" if ((length $lfn) < 1);
	die "LFN is not a clean string!" if ref $lfn;

	debug "TK:Obtain: obtain f:[$lfn] ($handler)($root)($path)- requested\n" if $tkdebug;

	my $ghostForLFN = $self->ghostForLFN();

	main::assert_cpath( $path );

	if( exists $ghostForLFN->{$lfn} )
	{
		debug "TK:Obtain: obtain f:[$lfn] - found in cache\n" if $tkdebug;
		return $ghostForLFN->{$lfn};		
	}

	# Next look for in global cache (if present)
	# TODO when time comes, implement global cache

	# X X X X X X X X 

	# Next look in local descriptor
	# TODO, this is sort of wrong -- the ghost store is useless for certain types of LFNS
	# really only things pretaining to file system should hit descriptor
	# refactor to move to FSE

	if( $handler eq "fs" )
	{
		my $gs = $self->getGhostStoreForDescriptorFileCoveringFN($path);
		my $g = $gs->findGhostForLFN($lfn);

		my $DID = ref $g ? $g->debugID() : 'Not found';
		debug "TK:Obtain: obtain f:[$lfn] pulled g:[$DID] from ghost descriptor\n" if $tkdebug;
		
		if( defined $g )
		{
			# note: registration already occured by getGhostStore...FN()
			return $g 
		}
	}

	# If this LFN is pointing to a container object, I think this is the
	# right place in the logic to force a load. This logic needs to be more
	# carefully considered.

	if( $handler eq "snap" )
	{
		debug "Obtain: container object load case" if $tkdebug; 
		
		my $g = Ghost->make_private( $lfn );
		# should this have been a virtual ghost?

		my $fse = FSEFactory->getFSEHandlerForGhost( $g );
		my $r = $fse->findGhostForLFN( $lfn );

		if( defined $r )
		{
			# note: registration already occured by getGhostStore...FN()
			return $r; 
		}
	}

	# Wow, we've totally stuck out here - make a new thing. When we get to
	# this point, how do we decide if this guy is going to be virtual or if
	# its going to be real?

	debug( "TK:Obtain: Making new ghost for f:[$lfn]\n")  if $tkdebug;
	my $g = Ghost->make_private( $lfn );
	# registers 
	$self->incorporateGhostData( $g );
	$g->dirty(1);
	$g->isTKversion(1);

	if( $handler eq "fs" )
	{
		my $gs = $self->getGhostStoreForDescriptorFileCoveringFN( $path );
		$gs->addGhost( $g );
	}

	my $DID = $g->debugID();
	debug "TK:Obtain: Made: $DID\n" if $tkdebug;

	return $g;
}


##########
#
# GhostIterator -- A helper for iterating over nested directories Note: Does
# not use File::Find! Everything is done through ghosts that request
# subdirectories so this is uber-clean.
#
# Recurse mode = everything goes back to TK
# NonRecurse Mode = only thigns in the parent are returned. 
#
####

package GhostIterator;

use File::Basename;
use base qw(Class::Accessor);
GhostIterator->mk_accessors( qw(queue recurse parentGhostStore) );

sub debug($)
{
	my $a = shift;
	main::debug( "GI: " . $a );
}

my $GI_DEBUG = 0; 

sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();
	$self->recurse(0);
	return $self;
}

# Gets the next item in the iterator
#
# Behind the scenes, will call nextRaw and handle things in the queue
# that are unexpected

sub next
{
	my $self = shift;

	return $self->nextRecurse() if( $self->recurse() );
	return $self->nextNoRecurse() if( !$self->recurse() );
	return;

}

sub nextNoRecurse
{
	my $self = shift;
	
	my $q = $self->queue();
	my $c = shift @$q;

	if( not defined $c )
	{
		return undef; 
	}

	return $self->parentGhostStore()->findGhostForLFN($c) ; 
	


}


sub nextRecurse
{
	my $self = shift;


	my $n = $self->nextRaw();

	if( ref $n )
	{
		debug "GI: THUNK received --> $$n\n" if $GI_DEBUG;
		return $self->next();
	}

	# Check for the end-of-queue case
	if( not defined $n )
	{
		debug "GI iterator: Undef case - iterator over\n" if $GI_DEBUG;
		return undef;
	}

	my $tk = TruthKeeper::get();
	my $g = $tk->obtainGhostForLFN( $n );

	return $g;
}

# Internal helper function called when a directory needs to be expanded

sub scanDir
{
	my $self = shift;
	my $g = shift;
 	my $q = $self->queue();	

	# call getChildren
	# getChildren may have these buggers cached
	# caching should be weak
	# if not call up to TK and try to get from GCIP
	# if not FSM->getImmediateChildren;

	my @lfns = $g->getFSE()->getImmediateChildren( $g );

	$g->learnChildrenByLFN( time(), @lfns );

	my @children = $g->getChildren();

 	unshift @$q, \$g;  # add the thunk that will tell us the directory is over
 	unshift @$q, @lfns; # add the files themselves
}

sub nextRaw
{
	my $self = shift;

	my $q = $self->queue();
	my $c = shift @$q;

	if( not defined $c )
	{
		return undef; 
	}

	if( ref $c )
	{
		return $c; 
	}

	# Todo - clean up passing of ghosts back up to parent
	# issue here is that the parentGhostStore only has the top
	# level parents.
	# my $gs = $self->parentGhoststore();
	# my $g = $gs->findGhostForLFN( $c );

	# TODO: two obtains are called, first to check for being a directory
	# second, by next. This could be refined by making the queue
	# able to store both ghosts and LFNs.

	my $tk = TruthKeeper::get();
	my $g = $tk->obtainGhostForLFN( $c );

	debug "GI: Checking [$c]->[$g] for children\n" if $GI_DEBUG;

	# if the next thing is a directory, scan into the directory
	if( $g->canHaveChildren() )
	{
		debug "GI: Scan directory case\n" if $GI_DEBUG;
		$self->scanDir( $g );
	}

	return $c;
}

##########
#
# GhostStore - utility functions for dealing with arbitrary collections
# of ghosts
#
####

package GhostStore;

use File::Basename;
use POSIX;
use base qw(Class::Accessor);
GhostStore->mk_accessors( qw(lfnToGhost sizeToFNs isDecending ) );

sub debug($)
{
	my $a = shift;
	main::debug( "GS: " . $a );
}

sub CODEPATH($)
{	
	my $p = shift; 
	main::CODEPATH( "GS: $p" );
}

sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();
	$self->lfnToGhost( +{} );
	$self->sizeToFNs( undef );
	$self->isDecending( 0 );

	return $self;
}

sub newDescending
{
	my $class = shift;
	my $self = $class->SUPER::new();
	$self->lfnToGhost( +{} );
	$self->sizeToFNs( undef );
	$self->isDecending( 1 );

	return $self;
}



## TODO

## ghost iterators now sort their contents why? well, turns out there is some
## non-deterministic behavior going on depending on how the values get scrabled
## into the cache and i need consistency. ideally though, we need a way of
## iterating through the ghost store in an order similiar to the one the user
## intendend.

sub getIterator
{
	my $self = shift;

	my $r = GhostIterator->new();
	$r->queue( [ sort keys %{$self->lfnToGhost()} ] );
	$r->recurse($self->isDecending() );
	$r->parentGhostStore( $self );
	return $r;
}

sub countDirty
{
	my $self = shift;

	my $i = 0;

	map {$i++ if $_->dirty() > 0 } $self->getAllGhosts();

	return $i;
}

sub findGhostForLFN
{
	my $self= shift;
	my $lfn = shift;

	my $lfnToGhost = $self->lfnToGhost();

	if(exists $lfnToGhost->{$lfn} )
	{
		debug "   Found inside ghoststore\n" if $gsDebug;
 		return $lfnToGhost->{$lfn};
 	}

 	return undef;
}

sub purifyAllGhostsToTK
{
	my $self = shift;
	my $tk = TruthKeeper::get();
	my $lfnToGhost = $self->lfnToGhost();

	foreach my $g (values %$lfnToGhost)
	{
		if( $g->isTKversion() == 0 )
		{
			debug "GhostStore: ***** PURIFY $g->lfn()\n" if $gsDebug;
			my $n = $tk->obtainGhostForLFN( $g->lfn() );
			$lfnToGhost->{$n->lfn()} = $n;
			CODEPATH( "GS purification");
		}
	}
}

sub printDebugShort
{
	my $self = shift;
		my $lfnToGhost = $self->lfnToGhost();
	print "GS", join ',', keys %$lfnToGhost;
}

sub printDebugMedium
{
	my $self = shift;
	my $lfnToGhost = $self->lfnToGhost();
	foreach my $g ($self->getAllGhosts() )
	{
		print "GS " . $g->debugID() . "  lfn:" . $g->lfn() . "\n";
		$g->printDebug();
	}
}


sub getAllGhosts
{
	my $self = shift;
	my $lfnToGhost = $self->lfnToGhost();
	return values %$lfnToGhost;
}

sub removeGhost($)
{
	my $self = shift;
	my $g = shift;

	my $lfn = $g->lfn();
	my $lfnToGhost = $self->lfnToGhost();

	my $DID = $g->debugID();
	debug "GhostStore: Remove ghost $DID (lfn=$lfn) from ghoststore\n" if $gsDebug;

	if( not exists $lfnToGhost->{$lfn} )
	{
		die "GhostStore: Boundary case - as to remove a ghost that doesn't exist";
	}

	delete $lfnToGhost->{$lfn};

	my $sizeToFNs = $self->sizeToFNs();

	# remove from size cache if it exists

	if( defined $sizeToFNs )
	{	
		if( defined $g->getSize() )
		{
			my $size = $g->getSize(); 
			my @r = grep { $lfn eq $_ } @{$sizeToFNs->{$size}};
			$sizeToFNs->{$size} = \@r;
		}
	}
}

sub addGhost($)
{
	my $self= shift;
	my $g = shift;

	my $lfn = $g->lfn();
	my $lfnToGhost = $self->lfnToGhost();

	warn "Something wrong lfn:$lfn" if $lfn eq ".";
	warn "Something wrong lfn:$lfn" if $lfn eq "..";

	my $DID = $g->debugID();
	debug "GhostStore: Adding ghost $DID (lfn=$lfn) to ghoststore\n" if $gsDebug;
	#debug "GhostStore: Adding ghost $DID (lfn=$lfn) (lfntoghosthash=$lfnToGhost gs_self=$self) ghoststore\n" if $gsDebug;

	if( exists $lfnToGhost->{$g->lfn()} )
	{
		die "GhostStore: Boundary case - adding ghost where one already existed";
	}

	$lfnToGhost->{$lfn} = $g; 

	$self->ghostStoreCacheTaint();

	return;
}

# Signal that membership in the store has changed, and that the caches 
# are probably not up to date. Conservative action is to revoke all caches.


sub ghostStoreCacheTaint
{
	my $self = shift;

	if( defined $self->sizeToFNs() )
	{
		CODEPATH( "Tainted ghoststore cache");
		$self->sizeToFNs( undef );
	}

}

# Pulls in the descriptor listed 

sub slurpDescriptor($)
{
	my $self = shift;
	my $fn = shift;

	debug "GhostStore: Slurping descriptor [$fn]\n" if $gsDebug;

	die "Assert: $fn should be a path" if not main::assert_regularpath( $fn );

	my( $parsedFn, $dir ) = fileparse( $fn );

	my $treebase = $dir; 

	$self->slurpSerializedStore( filename => $fn,
								 treeRoot => $treebase );

	# At this point, our ghosts are not linked to the TK, they are just versions of possible truth

	$main::run_descriptors_read++;
}

sub slurpSnapshot
{
	my $self = shift;
	my $fn = shift;
	my $root = shift;

	$self->slurpSerializedStore( filename => $fn,
								 immutable => 1, 
								 snapshotRoot => $root );
}


sub slurpSerializedStore
{
	my $self = shift;
	my %opt = @_;

	my $fn = $opt{filename} or die "Filename not provided to slurp!";

	my %seen;

	open( FILE, "<$fn") or die "Cannot open $fn to slurpSerializedStore";

	while( <FILE> )
	{
		chomp;
		debug "GhostStore: Slurping: [$_]\n" if $gsDebug;

		if( /^###/ )
		{
			next;
		}

		if( /^@@@\s*(\w+)\s*\=\s*(.*)/ )
		{
			my ($name, $value) = ($1,$2);
			debug "DESCRIPTOR: $name --> $value\n" if $gsDebug;

			if( $name eq 'version' )
			{
				debug "Comparing version $SERIALIZATION_VERSION_NUMBER to [$value]\n" if $gsDebug;

				if( $SERIALIZATION_VERSION_NUMBER != ($value+0) )
				{
					die "Version mismatch in $DESCRIPTOR_FILENAME: $SERIALIZATION_VERSION_NUMBER != [$value] -- the descriptor version is wrong. Delete the old descriptors to continue.";
				}
			}

			next;
		}

		my $g = Ghost->newFromSerialized( $_, %opt );

		$g->isTKversion(0);
		$self->addGhost( $g );

		die "Ghost descriptor contains duplicates - some logic problem - see [$fn]!" if( $seen{$g->lfn()}++ > 0 );
	}

	close( FILE );
}





sub writeAsDescriptor($)
{
	my $self = shift;
	my $fn = shift;

	my $lfnToGhost = $self->lfnToGhost();

	open( FILE, ">$fn") or die "Cannot open file[$fn] to write descriptor: $!";

	my $date = strftime( "%c", localtime() );

	print FILE "### This is a checksum tracking file written by $0\n";
	print FILE "### File written on $date\n";
	print FILE "### \n";
	print FILE "### To remove this and others like it, try this command\n";
	print FILE "###    find . -name '$DESCRIPTOR_FILENAME' -print0  | xargs -0 rm -iv\n";
	print FILE "### \n";
	print FILE "@@@ version = $SERIALIZATION_VERSION_NUMBER\n";
	print FILE "### \n";

	foreach my $g ( values %$lfnToGhost )
	{
		print FILE $g->serialize( pathMode => "basename");
		print FILE "\n";

		if( $gsDebug )
		{
			my $lfn = $g->lfn();
			debug "    WAD: Write ($lfn):".  $g->serialize( pathMode => "basename");
		}
	}

	close( FILE );

	debug "    WAD: Wrote descriptor -> $fn\n" if $gsDebug;

	$main::run_descriptors_write++;
}


sub writeSnapshotToDisk
{
	my $self = shift;
	my $fn = shift;

	$self->writeSerializedToDisk( $fn, pathMode => 'localized' );
}

#  TODO combine codebase with descriptors

sub writeSerializedToDisk($)
{
	my $self = shift;
	my $fn = shift;
	my %options = @_;

	my $lfnToGhost = $self->lfnToGhost();

	open( FILE, ">$fn") or die "Cannot open $fn to write snapshot or descriptor";

	my $date = strftime( "%c", localtime() );

	print FILE "### This is a snapshot, written by the tool $0\n";
	print FILE "### File written on $date\n";
	print FILE "### It is a record of all of the files stored under a tree at a single point in time\n";
	print FILE "### The gfs tool can use this to reconstruct this specific file tree later\n";
	print FILE "### \n";
	print FILE "@@@ version = $SERIALIZATION_VERSION_NUMBER\n";
	print FILE "### \n";

	foreach my $g ( values %$lfnToGhost )
	{
		print FILE $g->serialize( %options );
		print FILE "\n";
	}

	close( FILE );

	debug "Wrote snapshots -> $fn\n";
}

sub loadSizeCache
{
	my $self = shift;

	my $h = {};
	# my $tk = TruthKeeper::get();

	my $iterator = $self->getIterator();

	CODEPATH( "Gen size cache");

	debug "GS-Generating size cache\n";

	while( my $g = $iterator->next() )
	{
		#debug "GS-Generating size cache: $g\n";
		#$g->printDebug();

		next unless $g->isFile();
		my $size = $g->getSize();
		next if $size <= 0;

		if( $gsDebug )
		{
			my $lfn = $g->lfn();
			debug "GS - Size Cache lfn:[$lfn] -> $size\n" 
		}

		push @{$h->{$size}}, $g;
	}

	$self->sizeToFNs($h);
}

sub findFilesWithSize
{
	my $self = shift;
	my $s = shift;

	#debug "findFilesWithSize($s)";

	return undef if( $s <= 0 );

	if( not defined $self->sizeToFNs() )
	{
		$self->loadSizeCache();
	}

	my $lu = $self->sizeToFNs()->{$s};

	# debug "findFilesWithSize($s): $lu";

	return +() if not defined $lu;

	# Its theoretically possible the size cache is invalid. Quickly check our
	# answer. 

	my @c = @{$lu};
	my @r;
	foreach my $g (@c)
	{
		if( $g->size() != $s )
		{
			CODEPATH( "Rare case where ghoststore size cache returned stale data");
		}
		else
		{

			push @r, $g;
		}

	}




	return @r;
}

# Returns all ghosts other than the one passed that is a hash duplicate

sub findOtherDuplicates
{
	my $self = shift;
	my $g = shift;

	return grep { not $g->isSameFilename($_) } $self->findAllDuplicates($g);
}


# Returns every known ghost named in this collection that is a duplicate. A
# duplicate is defined as a file with the exact contents, even if things like
# mode or modification times differ.
#
# This is the most expensive duplicate finding operation since it is
# completionist -  all potential duplicates are verified. Note: duplicates may
# include  the ghost itself.

sub findAllDuplicates
{
	my $self = shift;
	my $target = shift;

	die "findAllDuplicates() passed invalid scalar" unless defined $target;

	my $size = $target->getSize();

	die "findAllDuplicates passed an invalide (size = undef) file" if not defined $size; 
	return () if $size < 1; 

	# First, check our size cache for a match
	my @otherFN = $self->findFilesWithSize( $size );

	return if @otherFN < 1;

    debug " -- Checking size duplicates size:[$size]\n" if $duplicateDebug;

	my @matches;

	foreach my $gc ( @otherFN )
	{
		#next if $gc->isSameFilename( $target );
		my $hit = 0;
		if( $target->isDuplicateOf( $gc ) )
		{
			$hit = 1;
			push @matches, $gc;
		}


		if( $duplicateDebug )
		# if( 1 )
		{
			my $cFN = $gc->lfn();
			my $tFN = $target->lfn();

			my $cD = $gc->quickDebugString();
			my $tD = $target->quickDebugString();
			


				debug "     -- Checking size duplicates size:[$size]\n";
				debug "     ---> cfn:[$cFN] $cD " .($hit?"HIT=Y":"HIT=N"). "\n";
				debug "  vs ---> tfn:[$tFN] $tD\n";
				
		}

	
	}

	return @matches;

	# return @matches;
}

# Returns a single ghost that is a duplicate, or undef if no such duplicate
# exists. In theory this can be implemented in a "leaner" way since once a
# single is found, no more hash duplicates need to be tested.

sub findSingleDuplicate
{
	my $self = shift;
	my $target = shift;

	my @r = $self->findOtherDuplicates( $target );

	return undef if @r < 1;
	return pop @r; 
}

sub findSingleDuplicateIncludingSelf
{
	my $self = shift;
	my $target = shift;

	my @r = $self->findAllDuplicates( $target );

	return undef if @r < 1;

	return pop @r; 
}

sub hasDuplicate
{
	my $self = shift;
	my $target = shift;

	# ideally if the size is not present, doesn't force any hashes
	# should check for that

	return $self->findSingleDuplicate($target);



}


##########
#
# Ghost - an imprint of a file that may or may not exist at a particular place
#
# The ghost is a carrier of information about a file, and intended to be
# lightweight More than one ghost can exist for a given file, and they can be
# different. To obtain an "authoritiative" ghost, the TruthKeeper is used to
# resolve them for instance a snapshot could have one version of a ghost, a
# descriptor a different and the one from the file system could yet again be
# different. Ghosts are intended to be so lightweight that they can be used as
# proxies for filename everywhere as of the "everthing-is-a-ghost" update,
# ghosts are used universally to handle files.
#
# Ghosts are never expected to change LFNs under any circumstances. The only
# acceptable time to assing an LFN is during deserialization or creation.
#
# Immutability is a key aspect of ghosts; an immutable ghost does not change
# any of its fields. Normal ghosts are expected to update themselves based on
# filesystem or data state. There are a set of atomic definable rules that say
# when new types of state refresh each ghost.#
#
# The ghost class does not itself hit the disk - everything is delegated to
# the FSE. This allows multiple different handlers to interpret the LFN the
# way they see fit.
#
# Terminology used in function names has some nuance
#
# -     size() - the internal size descriptor, never use this!!
# GET   getSize - gets an ACCEPTABLE size
# LEARN learnSize - tell me about new information pretaining to this ghost
#

package Ghost;

use File::Basename;
use base qw(Class::Accessor);

use Cwd qw(abs_path); # best way to get these



# GHOST_NEW_FIELD: Place 4/4 where you add new fields to ghosts

Ghost->mk_accessors( qw(lfn lastFullHash size mode fullHash statStruct
	dirty isTKversion lastDirScan lastStat mtime lastEnumerated firstStat fileStatus
	 children _immutable _debugID) );


my $GHOST_DEBUG_ID = 100000; 


sub new
{
	my $class = shift;
	my $self = $class->SUPER::new();

	$self->_debugID( $GHOST_DEBUG_ID++ );

	return $self;
}

sub clone
{
	my $self = shift;

	my $c = $self->new();

	$c->pullValuesFromGhost( $self );
	
	return $c;
}

sub cloneWithLFN
{
	my $self = shift;
	my $lfn = shift;

	my $copy = $self->clone();

	# print "Clone $lfn\n";
	$copy->lfn( $lfn );

	return $copy;
}


sub setImmutable
{
	my $self = shift;
	$self->_immutable(1);
}


sub pullValuesFromGhost
{
	my $self = shift;
	my $target = shift;

	# GHOST_NEW_FIELD: Place 5/5 where you add new fields to ghosts

	$self->lfn( 		 $target->lfn() );
	$self->lastFullHash( $target->lastFullHash() );
	$self->size( 		 $target->size() );
	$self->mode( 		 $target->mode() );
	$self->fullHash( 	 $target->fullHash() );
	$self->lastDirScan(  $target->lastDirScan() );
	$self->lastStat( 		$target->lastStat() );
	$self->lastEnumerated( 		$target->lastEnumerated() );
	$self->firstStat( 		$target->firstStat() );
	$self->fileStatus( 		$target->fileStatus() );
	$self->mtime( 		$target->mtime() );
	$self->statStruct( 		$target->statStruct()->copy() );

	$self->dirty( 0 );
	$self->isTKversion( 0 );
}


sub debug($)
{
	my $a = shift;
	main::debug( "G: " . $a );
}

sub CODEPATH($)
{	
	my $p = shift; 
	main::CODEPATH( "Ghost: $p" );
}

sub debugID()
{
	my $self = shift;
	return $self->_debugID();
}

sub newFromSerialized($)
{
	my $class = shift;
	my $str = shift;
	my %options = @_;

	my $self = Ghost->new();

	my @a = split ':', $str;

	debug "Pre-GUS:" . join '|', @a if $granularSerialDebug;

	@a = map {main::gfs_unescape($_)} @a;

	debug "Post-GUS:" . join '|', map { defined $_ ? $_ : "undef" } @a if $granularSerialDebug;	

	my $serialLFN = shift @a;

	if( defined $options{'treeRoot'} )
	{
		my $root = $options{'treeRoot'};

		$serialLFN = main::rewriteLFNwithLeadingDirectory( $serialLFN, $root );
	}

	if( exists $options{snapshotRoot} )
	{
		my $newRoot = $options{snapshotRoot};

		$serialLFN = main::rewriteLFNreplaceRoot( $serialLFN, $newRoot );
	}


	if( defined $options{'immutable'} && $options{'immutable'} )
	{
		$self->_immutable(1);
	}

	# GHOST_NEW_FIELD: Place 1/4 where you add new fields to ghosts

	$self->lfn( $serialLFN );
	$self->lastFullHash( shift @a );
	$self->size( shift @a );
	$self->mode( shift @a );
	$self->fullHash( shift @a );
	$self->lastDirScan( shift @a );
	$self->lastStat( shift @a );
	$self->lastEnumerated( shift @a );
	$self->firstStat( shift @a );
	$self->fileStatus( shift @a );
	$self->mtime( shift @a );

	my $statStructSerialized = shift @a; 
	if( defined $statStructSerialized )
	{
		my $statStruct = StatStruct->new();
		$statStruct->deserialize( $statStructSerialized );
		$self->statStruct( $statStruct );
	}
	else
	{
		$self->statStruct( undef );
	}


	$self->dirty( 0 );
	$self->isTKversion( 0 );

	if( $serializeDebug )
	{
		debug "Just deserialized: " . $self->makeDebugString();
	}	

	return $self;
}

sub isImmutable
{
	my $self = shift;

	return 0 if not defined  $self->_immutable();
	return 1 if  $self->_immutable() > 0 ;
	return 0; 
}

# getFSE - returns whatever FSE object is best for this thing

sub getFSE
{
	my $self = shift;
	my $lfn = $self->lfn();
	die unless defined $lfn;

	my $fse = FSEFactory->getFSEHandlerForGhost( $self );
	return $fse; 
}

sub getChildren()
{
	my $self = shift;

	if( not defined $self->children() )
	{
		die "Corner case - someone didn't teach me my children";
	}

	if( not ref $self->children() )
	{
		die "Corner case - someone didn't teach me my children";
	}

	# TODO - should this be weak?
	# if so, how to handle orphans?
	return @{$self->children()};
}

# Caller is giving us a complete list of our children from primary source
# Mark the deleted ones and perform other flags

sub learnChildrenByLFN()
{
	my $self = shift;
	my $time = shift;

	my @lfns = @_;

	my $tk = TruthKeeper::get();

	# Ideally, we call getChildren and do some sort of "merge"

	my @existingGhosts =  ref $self->children() ? @{$self->children()} : ();

	debug "Existing ghosts @existingGhosts\n" if $gChildDebug;

	my %seenLFN;
	my @newKids;

	foreach my $e (@existingGhosts)
	{
		 $seenLFN{ $e->lfn() } = 0;
	}

	foreach my $lfn ( @lfns )
	{
		debug "G: Learning child: lfn:[$lfn]\n" if $gChildDebug;
		if( exists $seenLFN{$lfn} )
		{
			debug "G:   Child already registered\n" if $gChildDebug;
		}

		$seenLFN{$lfn}++;

		my $g = $tk->obtainGhostForLFN( $lfn );
	
		# inform ghost that it has been seen in a directory scan
		# this also undeletes it
		$g->learnDirscan($time);
		push @newKids, $g;
	}

	foreach my $lfn ( keys %seenLFN )
	{
		if( $seenLFN{$lfn} < 1 )
		{
			debug "G:   Child has disapeared\n" if $gChildDebug;
			my $g = $self->tk()->obtainGhostForLFN( $lfn );
			$g->learnDeleted($time);
		}
	}

	$self->children( \@newKids );
}

# canHaveChildren() - True if we can descend with a call to "getChildren"

sub canHaveChildren
{
	my $self = shift;

	# debug "Fetching type\n";

	my $type = $self->getType();

	# debug "Type is: $type\n";

	# base case: no knowledge or hash

	if( $type eq 'd' )
	{
		return 1;
	}

	return 0;
}


sub isSingleFile
{
	my $self = shift;
	my $type = $self->getType();
	return 1 if $type eq 'f';
	return 0; 
}

sub printDebug
{
	my $self = shift;

	debug $self->makeDebugString();

}

sub quickDebugString
{
	my $self = shift;

	my $debugID = $self->_debugID();
	my $size 			= defined( $self->size() ) 		? $self->size() : "_";
my $lfn 			= defined( $self->lfn() ) 		? $self->lfn() : "_";
# my $immutable 		= defined( $self->_immutable() ) 		? $self->_immutable() : "_";
	my $fullHash 		= defined( $self->fullHash() ) 		? $self->fullHash() : "_";

		$fullHash =~ /(\w{5,5})$/;
		my $fh = $1 || "_";
		my $ix = defined $self->_immutable() ? ($self->_immutable() >0 ? "I+" : "I-" ) : 'I_'; 

		return "$debugID/$size/$fh/$ix/$lfn"


}

sub makeDebugString
{
	my $self = shift;

	# GHOST_NEW_FIELD: Place 2/4 where you add new fields to ghosts

	# Warning: Don't fire of any  "getXXX" here
	no warnings;

	my $lfn 			= defined( $self->lfn() ) 		? $self->lfn() : "undef";
	my $lastFullHash 	= defined( $self->lastFullHash() ) 		? $self->lastFullHash() : "undef";
	my $size 			= defined( $self->size() ) 		? $self->size() : "undef";
	my $mode 			= defined( $self->mode() ) 		? $self->mode() : "undef";
	my $fullHash 		= defined( $self->fullHash() ) 		? $self->fullHash() : "undef";
	my $lastDirScan 	= defined( $self->lastDirScan() ) 		? $self->lastDirScan() || '<none>' : "undef";
	my $lastStat 		= defined( $self->lastStat() ) 		? $self->lastStat() : "undef";
	my $mtime 			= defined( $self->mtime() ) 		? $self->mtime() : "undef";
	my $lastEnumerated 	= defined( $self->lastEnumerated() ) 		? $self->lastEnumerated() : "undef";
	my $firstStat 		= defined( $self->firstStat() ) 		? $self->firstStat() : "undef";
	my $fileStatus 		= defined( $self->fileStatus() ) 		? $self->fileStatus() : "undef";
	my $immutable 		= defined( $self->_immutable() ) 		? $self->_immutable() : "undef";
	my $statStruct 		= defined( $self->statStruct() ) 		? $self->statStruct()->serialize() : "undef";

	# not technically a field but we should include this

	my $type = defined $self->mode() ? $self->extractTypeFromMode( $self->mode() ) : "???";
	# my $type = $self->getType();

my $debugID = $self->_debugID();

	return "ghostDump($debugID): lfn:[$lfn] type:[$type] mode:[$mode] l_FH:[$lastFullHash] ".
	"full:[$fullHash] ldirscan:[$lastDirScan] l_stat:[$lastStat] size:[$size]".
	" mtime:[$mtime] immutable[$immutable] statStruct:[$statStruct]";


}

sub isSameFilename
{
	my $self = shift;
	my $other = shift;

	my $fn1 = $self->lfn();
	my $fn2 = $other->lfn();

	# After some testing, it appears that abs_path (also see realpath(3)) 
	# is the very best way to test for complete filename equivilence
	# The testing read-out is below and turned off for now

	if( 0 )
	{
		debug "Checking if two files are the same filename:\n";
		debug "   me: [$fn1]\n";
		debug "   tg: [$fn2]\n";
	  
	  	my $cwd1 = abs_path( $fn1 );
	  	my $cwd2 = abs_path( $fn2 );

	 	debug "Using Cwd Abs_path:\n"; 
	  	debug "   me: [$cwd1]\n";
		debug "   tg: [$cwd2]\n";

	  	my $abs1 = File::Spec->rel2abs( $fn1 );
	  	my $abs2 = File::Spec->rel2abs( $fn2 );

	 	debug "Using rel2abs:\n"; 
	  	debug "   me: [$abs1]\n";
		debug "   tg: [$abs2]\n";
	}

	die "Incomplete info self-size" if not defined $self->lfn();
	die "Incomplete info other-size" if not defined $other->lfn();

	# TODO: revisit if perl libraries give a better way to do this

	return 1 if $self->absoluteLFN() eq $other->absoluteLFN();
	return 0;
}


sub absoluteLFN
{
	my $self = shift;
	return $self->getFSE()->getAbsoluteLFNForGhost($self);
}

sub isDuplicateOf
{
	my $self = shift;
	my $other = shift;

	die "Incomplete info self-size" if not defined $self->size();
	die "Incomplete info other-size" if not defined $other->size();

	return 0 if( $self->getSize() ne $other->getSize() );

#   die "Incomplete info self full hash" if not defined $self->fullHash();
#	die "Incomplete info other full hash " if not defined $other->fullHash();

		my $mH = $self->getFullHash();
		my $oH = $other->getFullHash();

	if( $duplicateDebug )
	{

		my $mI = $self->debugID();
		my $oI = $other->debugID();
		debug "isDuplicateOf - forcing hash $mH - $oH ($mI and $oI)\n";
	}
	return 1 if( $mH eq $oH );

	return 0; 
}

sub serialize()
{
	my $self = shift;
	my %options = @_;

	my $lfn = $self->lfn();

	#debug "Serialize $self\n";

	if( exists $options{'pathMode'} and $options{'pathMode'} eq 'basename' )
	{
		$lfn = main::rewriteLFNPathToBasename( $lfn )
	}

	my $statStructSerialized = undef; 
	if( defined $self->statStruct() )
	{
		$statStructSerialized = $self->statStruct()->serialize();
	}

	# GHOST_NEW_FIELD: Place 3/4 where you add new fields to ghosts

	my @s = ( $lfn, 
			  $self->lastFullHash(),
			  $self->size(),
			  $self->mode(),
			  $self->fullHash(),
			  $self->lastDirScan(),
			  $self->lastStat(),
			  $self->lastEnumerated(),
			  $self->firstStat(),
			  $self->fileStatus(),
			  $self->mtime(),
			  $statStructSerialized
			  );

	debug "PRE-GFSE:" . join '|', map { defined $_ ? $_ : "undef" } @s if $granularSerialDebug;	

	@s = map {main::gfs_escape($_)} @s;

	debug "Post-GFSE:" . join '|', map { defined $_ ? $_ : "undef" } @s if $granularSerialDebug;	


	my $s = join ':', @s;

	return $s;
}

# as of refactor, only TK is really allowed to called this

sub getHandler
{
	my $self = shift;
	my ($handler, $root, $path ) = main::parseFQLFN( $self->lfn() );
	return $handler;
}

sub getRoot
{
	my $self = shift;
	my ($handler, $root, $path ) = main::parseFQLFN( $self->lfn() );
	return $root;
}

sub getPath
{
	my $self = shift;
	my ($handler, $root, $path ) = main::parseFQLFN( $self->lfn() );
	return $path;
}

sub getLFNcompontents
{
	my $self = shift;
	my ($handler, $root, $path ) = main::parseFQLFN( $self->lfn() );
	return ($handler, $root, $path);
}

sub make_private($)
{
	my $class = shift;
	my $lfn = shift;

	my $self = $class->new();

	# debug "Ghost: Making ghost for lfn=[$lfn]\n";

	$self->lfn( $lfn );

	return $self;
}

sub make
{
	die "Assert - old code, warning!!";
}

# Forces the key fields to be loaded if they aren't already known
# Does not recheck them against the disk unless the standard

# TODO: implement size and fasthash

sub forceCharacterization
{
	my $self = shift;

	$self->getFullHash();
	$self->getSize();
	# $self->getFastHash();	
}

sub hasAcceptableFullhash
{
	my $self = shift;
	my $timeguard = shift;




	# debug "Checking if I like my hash\n";

	if( $self->isImmutable() )
	{
		#debug "I'm immutable - i like my hash";
		return 1 ;

 	}

	if( not defined $self->fullHash() or $self->fullHash() eq "" )
	{
		#debug "has not defined, reject";
		return 0;
	}

	if( defined $timeguard && $self->lastStat() < $timeguard )
	{
		CODEPATH( "invalidating fullHash due to timeguard" );
		return 0;
	}
	
	if( defined $main::GUARD_TIME_SECONDS )
	{
		my $age = $main::SCRIPT_START - $self->lastFullHash();

		if( $age > $main::GUARD_TIME_SECONDS )
		{
			CODEPATH( "Hash too old, recheck forced" );
			debug ( join '', "Forcing hash recalculation (", main::expressRelativeTime( $age ), ")\n");
			return 0; 
		}
	}

	# debug "Yes, hash is OK.";

	return 1;
}

# returns an acceptable fullHash for the ghost
# Normally, the latest cached data is trusted, but
# this function can be set to take a out of data criteria in future TODO

sub getFullHash
{
	my $self = shift;

	if( $self->hasAcceptableFullhash() )
	{
		return $self->fullHash();
	}
	else
	{
		# The key strategy here is to stat the file twice before and after.
		# This ensures that the file isn't changing underneath us

		my $t = time();

		$self->getStatSourceVerify( $t );
		return undef if not $self->exists();
		$self->getFullHashSourceVerify( $t );
		$self->getStatSourceVerify( $t );

		return $self->fullHash(); 
	}
}


sub hasAcceptableStat
{
	my $self = shift;
	my $timeguard = shift;

	return 1 if $self->isImmutable();

	return 0 if( not defined $self->lastStat() );

	if( defined $timeguard && $self->lastStat() < $timeguard )
	{
		CODEPATH( "invalidating stat due to timeguard" );
		return 0;
	}

	if( defined $main::GUARD_TIME_SECONDS )
	{
		my $age = $main::SCRIPT_START - $self->lastStat();

		if( $age > $main::GUARD_TIME_SECONDS )
		{
			CODEPATH( "Hash too old, recheck forced" );
			debug (join '', "Forcing recalculation (", main::expressRelativeTime( $age ), ")\n" );
			return 0; 
		}
	}

	return 1;
}

# TODO factor out hasAcceptable patttern into "bringStatToDateIfNeeded()"


sub getMode
{
	my $self = shift;

	if( $self->hasAcceptableStat() )
	{
		return $self->mode();
	}
	else
	{
		$self->getStatSourceVerify( time() );
		return $self->mode();
	}	
}


sub getSize
{
	my $self = shift;

	if( $self->hasAcceptableStat() )
	{
		return $self->size();
	}
	else
	{
		$self->getStatSourceVerify( time() );
		return $self->size();
	}	
}

sub getMTime
{
	my $self = shift;

	if( $self->hasAcceptableStat() )
	{
		return $self->mtime();
	}
	else
	{
		$self->getStatSourceVerify( time() );
		return $self->mtime();
	}	
}

sub verifyGhostIsMutable
{
	my $self = shift;
	die "Mutable operation attempted on immutable ghost!" if $self->isImmutable();
}


###########
# Storage semantics
#
# Represents the idea that you want the thing pointed to by this ghost to assume
# the identical contents of the ghost being pointed to. This is one of those
# wonderful things that we just pass over to the FSE.
#

sub store
{
	my $self = shift;
	my $thing = shift;

	return $self->getFSE()->store( $thing, $self );
}

######


sub moveGhost
{
	my $self = shift;
	my $thing = shift;

	return $self->getFSE()->moveGhost( $self, $thing );
}


###########
# Getting a hardfile
#
# Gets you a path with that object that is free and clear for readonly
# purposes. Fills in the gap that possibly a file is on a remote server.
#

sub getReadonlyHardFile
{
	my $self = shift;

	return $self->getFSE()->getReadonlyHardFile($self);
}





sub generateAlternativeLocation
{
	my $self = shift;
	my $strategy = shift; 

	return $self->getFSE()->generateAlternativeLocation($self, $strategy);
}


###########
# Tests for existence
#
# The exists() semantic tells you definitively if the file is present or not within
# the window of knowledge of the time range given. Default does not actually hit the disk.
#
# 

sub exists()
{
	my $self = shift;
	my $time = shift;


	if( $self->isImmutable() )
	{
		warn "Untested case of exists on something immutable()";
		return 1; 
	}

	return $self->getStatPresence($time);
}


sub getStatPresence
{	
	my $self = shift;
	my $time = shift;  # ok to be undefined

	if( not $self->hasAcceptableStat($time) )
	{
		$self->getStatSourceVerify( time() );
	}

	if( main::statModeParse_isError( $self->mode() ) )
	{
		return 0; 
	}

	return 1; 
}


# ghosts nee 


###########
# The Learning Rules for Ghosts
#
# A set of rules govorn how new information is learned from an outside source.
# All truth state merger information happens here. The core rules have been worked
# out in a mini-proof in the CONCEPTS.txt document. 
#
# RULE #1: A stat can never be older than the hash in a given ghost. 
# Cause for immediate program termination.
# 
# RULE #2: Two peices of information with the same date must be the same, otherwise ERROR
# 
# RULE #3: The date corresponding to the size is lockstep with the date on rest of the stat. Correlary -- all stat information must travel together
# 
# RULE #4: A new provided hash will be accepted into a learner as long as its date 
# is newer than both the hash and the stat
# 
# RULE #5: A new stat that represents a change in size should invalidate the
# existing hash, unless the hash has the exact same date as the stat. (If the existing
# hash is newer than the incoming stat, but older than the existing stat, we
# have a logical violation of RULE #1)
#
# RULE #6: New stat overrides old stat
#
# RULE #7: A dead stat (ERR) always revokes the hash and blanks out size and XXX

# Policy: same information with a new date does not normally force dirtyness
# unless it is a hash, because the hash is expensive
#


sub learnFromGhost
{
	my $self = shift; 
	my $g = shift;

	die unless ref $g; 

	CODEPATH( "ghost merging" );

	if( defined $g->lastStat() )
	{
		my $size = $g->size();
		my $mode = $g->mode();
		my $mdate = $g->mdate();
		my $statStruct = $g->statStruct();

		$self->learnStat( $g->lastStat(), $size, $mode, $mdate, $statStruct );
	}

	if( defined $g->lastFullHash() )
	{
		my $hash = $g->fullHash();
		$self->learnFullHash( $g->lastFullHash(), $hash );
	}

}




# Should undelete the file

sub learnPresent
{
	my $self = shift;
	my $time = shift; 

	$self->verifyGhostIsMutable();

	# TODO: implement field
}





# Dirscan means that we have seen the ghost in a directory scan
# TODO also update a "firstSeen" field.
# If deleted, this should undelete by delegating to learnPresent

sub learnDirscan
{
	my $self = shift;
	my $time = shift; 

	$self->verifyGhostIsMutable();

	$self->lastDirScan( $time );

	# TODO: Do we want a dirscan to always force an update to the ghost?
	# With this next line commented out, a new directory scan will not by itself
	# force an update
	# $self->dirty(1);

	$self->learnPresent( $time );
}

# This tells the ghost that the file contents are presumed to have changed. In
# fact, the file may not exist anymore. For consistency, we basically pretend
# that the file doesn't exist anymore, thus forcing recalculations.
# hasAcceptableXXX() will always return false in these cases.

# TODO: is a cleaner way to do this to have some sort of "knownUpdate" field
# that is triggered on it which automatically fails out the next hasAcceptable() 

sub learnContentsChanged
{
	my $self = shift;
	my $effectiveTime = shift;

	$self->size( undef );
	$self->mtime( undef );
	$self->fullHash( undef );
	$self->mode( undef );
	$self->lastFullHash( undef );
	$self->lastStat( undef );
}

# This is the universal way of reporting new stat information, either from the
# disk, or from another ghost. It adjudicates what information to keep and
# what to toss using the set of rules worked out above. 

sub learnStat
{
	my $self = shift;
	my $time = shift;

	die if $self->isImmutable();

	die "learnStat() did not get a time argument" unless defined $time;
	die "incoming information newer than current time ($time)" if $time > time();

	my $size = shift;
	my $mtime = shift;
	my $mode = shift;
	my $statStruct = shift;

	die "invalid call to learnStat" if defined $statStruct and not ref $statStruct;

	my $q = $self->quickDebugString();

	my $__size = defined $size ? $size : "<undef>";
	my $__mtime = defined $mtime ? $mtime : "<undef>";
	my $__mode = defined $mode ? $mode : "<undef>";
	my $__statStruct = defined $statStruct ? $statStruct : "<undef>";
	print "LEARN [$__size], [$__mtime], [$__mode] [$__statStruct] for $q\n";
	$self->printDebug();

	# RULE3: ALl stat informaiton must travel together. 

	die "learn() got partial stat (violates RULE#3)" if not defined $mode;

	if( not main::statModeParse_isError( $mode ) )
	{
		die "learn() got partial stat (violates RULE#3)" if not defined $size;
		die "learn() got partial stat (violates RULE#3)" if not defined $mtime;
	}

	$self->verifyGhostIsMutable();

	# RULE1: t(stat) > t(hash) or BOMB

	if( (defined $self->lastFullHash()) && ($time < $self->lastFullHash() ))
	{
		die "RULE 1: RULE1: t(stat) > t(hash) or BOMB";
	}

	# RULE2: incoming information with same date as existing information must be same

	if( (defined $self->lastStat()) and ($time == $self->lastStat() ))
	{

		die "Rule2: same date mismatch A" if defined $size  and defined $self->size()  and $self->size() != $size;
		die "Rule2: same date mismatch B" if defined $mtime and defined $self->mtime() and $self->mtime() != $mtime;
		die "Rule2: same date mismatch C" if defined $mode  and defined $self->mode()  and $self->mode() ne $mode;
		# its okay for stat structs to differ slightly


		die "Rule2: same date mismatch E" if defined $size  and not defined $self->size(); 
		die "Rule2: same date mismatch F" if defined $mtime and not defined $self->mtime();
		die "Rule2: same date mismatch G" if defined $mode  and not defined $self->mode();
		die "Rule2: same date mismatch H" if defined $statStruct  and not defined $self->statStruct();
	}

	# RULE #6: Error stats revoke all other fields

	if( main::statModeParse_isError( $mode ) )
	{
		$self->lastFullHash(undef);
		$self->fullHash(undef);

		CODEPATH( "revoke hash due to stat error" );		
	}

	# RULE #5: A new stat that represents a change in size should invalidate the
	# existing hash, unless the hash has the exact same date as the stat. (If the existing
	# hash is newer than the incoming stat, but older than the existing stat, we
	# have a logical violation of RULE #1)

	if( (defined $self->size()) && ((not defined $size) or ($self->size() != $size)))
	{
		$self->lastFullHash(undef);
		$self->fullHash(undef);

		CODEPATH( "revokehash with new size" );
	}

	if( (defined $self->lastStat()) and ($time < $self->lastStat() ))
	{
		CODEPATH( "ignored older incoming stat" );
		return;
	}

	my $anythingHasChanged = 0;

	if( 
		(not defined $self->lastStat()   ) or 
		(not defined $self->size()       ) or 
		(not defined $self->mtime()      ) or 
		(not defined $self->mode()       ) or
		(not defined $self->statStruct() )
		)
	{
		$anythingHasChanged = 1; 
	}
	else
	{
		$anythingHasChanged = 1 if( ( not defined $size  ) and ( defined $self->size()  ) );
		$anythingHasChanged = 1 if( ( not defined $mtime ) and ( defined $self->mtime() ) );
		$anythingHasChanged = 1 if( ( not defined $mode  ) and ( defined $self->mode()  ) );
		$anythingHasChanged = 1 if( ( not defined $statStruct  ) and ( defined $self->statStruct()  ) );


		$anythingHasChanged = 1 if( defined $size and $self->size() != $size );
		$anythingHasChanged = 1 if( defined $mtime and $self->mtime() != $mtime );
		$anythingHasChanged = 1 if( defined $mode and $self->mode() ne $mode );
		$anythingHasChanged = 1 if( defined $statStruct and $self->statStruct()->hasMeaningfullyChanged( $statStruct ) );
	}

	if( (defined $self->lastStat()) and ($time > $self->lastStat() ))
	{
		CODEPATH( "Newer stat information accepted");
	}	

	$self->size($size);
	$self->mtime($mtime);
	$self->mode($mode);
	$self->statStruct($statStruct);

	$self->lastStat( $time );

	if(( $anythingHasChanged == 0 ) and ($time > $self->lastStat() ))
	{
		CODEPATH( "new stat did not mark dirty" );
	}

	$self->dirty(1) if $anythingHasChanged;
}

sub learnFullHash
{
	my $self = shift;
	my $time = shift;
	my $hash = shift;

	die if $self->isImmutable();


	die unless defined $time;
	die unless defined $hash;

	die "incoming information newer than current time ($time)" if $time > time();

	$self->verifyGhostIsMutable();

	# RULE 1: A stat can never be older than the hash in a given ghost

	if( defined $self->lastFullHash() and defined $self->lastStat() )
	{	
		die "stat cannot be older than hash (assert)" unless $self->lastFullHash() <= $self->lastStat();
	}	

	# Check rule 1 and rule 2

	if( defined $self->lastFullHash() )
	{
		# RULE 1: A stat can never be older than the hash in a given ghost

		die "stat cannot be older than hash" unless $self->lastFullHash() <= $time;
	
		# Rule 2: New information with same date must be same information

		if( $self->lastFullHash() == $time )
		{
			die "Rule 2 violation" unless  $self->fullHash() eq $hash; 
		}
	}

	# Avoid doing anything if the hash is older

	if( (defined $self->lastFullHash()) and ($time < $self->lastFullHash() ))
	{
		CODEPATH( "Ignored older hash");
		return; 
	}	

	if( (defined $self->fullHash()) and ($hash ne $self->fullHash() ))
	{
		CODEPATH( "Newer hash information accepted"); 
	}

	if( !(defined $self->fullHash()))
	{
		CODEPATH( "First hash information accepted"); 
	}		

	$self->lastFullHash( $time );
	$self->fullHash( $hash );
	$self->dirty(1);
}

# Deleted means that we did not see the ghost on a recent scan. Mark as
# deleted and set deleteDate.

# TODO change semantics, this is not sort of like an confimed negative

sub learnDeleted
{
	my $self = shift;
	my $time = shift; 

	$self->verifyGhostIsMutable();

	$self->lastDirScan( $time );

	# TODO: Do we want a dirscan to always force an update to the ghost?
	$self->dirty(1);
}

use Fcntl ':mode';

sub extractTypeFromMode
{
	my $self = shift;
	my $mode = shift;

	# die if $mode eq "";
	# die if not defined $mode;
	# print "Mode:: [$mode]\n";

	return undef if( not defined $mode );

	return '_' if main::statModeParse_isError( $mode );

	return 'f' if S_ISREG($mode);
	return 'd' if S_ISDIR($mode);

	return undef;
}

sub isFile
{
	my $self = shift;
	die if not defined $self->getMode();
	return 1 if $self->getType() eq 'f';
}

sub getType
{
	my $self = shift;
	my $mode = $self->getMode();
	return $self->extractTypeFromMode( $mode );
}


sub getFullHashSourceVerify
{
	my $self = shift;
	my $time = shift;

	die "Full hash not implemented for directories" if( $self->canHaveChildren() );

	my $fullhash = $self->getFSE()->getFullHashSourceVerify( $self, $time );
 	$self->learnFullHash( $time, $fullhash );

	return $fullhash;
}


sub getStatSourceVerify
{
	my $self = shift;
	my $time = shift; 

	my ($size, $mtime, $mode, $statStruct) = $self->getFSE()->getStatSourceVerify( $self, $time );
 
	$self->learnStat( $time, $size, $mtime, $mode, $statStruct );

	return ( $size, $mtime, $mode );
}

###

main::main();
exit();

